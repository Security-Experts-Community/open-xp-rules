event Create_user_winevent:
    key:
        event_src.host
    filter {
        filter::NotFromCorrelator()
        and lower(event_src.subsys) == "security"
        and msgid == "4720"
        and not match(object.account.name, "*\$") # исключаем создание аккаунта, оканчивающегося на $ 
        and object.account.fullname == "-"        # отстусвует upn
    }
    
event Create_user_registry:
    key:
        event_src.host
    filter {
        filter::NotFromCorrelator()
        and 
        (
            (
                lower(event_src.title) == "sysmon"
                and msgid == "12"
            )
            or 
            (
                lower(event_src.title) == "security"
                and msgid == "4657"
            )
        )
        and action == "create"
        and status == "success"
        and object.path == "\\registry\\machine\\sam\\sam\\domains\\account\\users\\names\\"
        and not match(object.name, "*\$")
    }

rule Create_local_account: Create_user_winevent or Create_user_registry

    on Create_user_winevent {
        $action = action 

        $event_src.ip = event_src.ip
        $event_src.hostname = event_src.hostname
        $event_src.host = event_src.host
        $event_src.asset = event_src.asset
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.vendor = event_src.vendor
        $event_src.category = event_src.category
        $event_src.rule = event_src.rule

        $object = object
        $object.account.name = object.account.name
        $object.account.fullname = object.account.fullname
        $object.account.domain = object.account.domain
        $object.account.session_id = object.account.session_id
        $object.account.id = object.account.id
        $object.account.privileges = object.account.privileges

        $subject = subject
        $subject.account.name = subject.account.name
        $subject.account.fullname = subject.account.fullname
        $subject.account.domain = subject.account.domain
        $subject.account.session_id = subject.account.session_id
        $subject.account.id = subject.account.id
        $subject.account.privileges = subject.account.privileges
    }

    on Create_user_registry {
         $action = action
        $status = status

        $subject.account.id = subject.account.id
        $subject.account.domain = subject.account.domain
        $subject.account.fullname = subject.account.fullname
        $subject.account.name = subject.account.name
        $subject.account.session_id = subject.account.session_id
        
        $subject.process.id = subject.process.id
        $subject.process.name = subject.process.name
        $subject.process.path = subject.process.path
        $subject.process.fullpath = subject.process.fullpath
        $subject.process.guid = subject.process.guid

        $object.account.id = object.account.id
        $object.account.name = object.account.name
        $object.account.domain = object.account.domain
        $object.account.session_id = object.account.session_id
        $object.account.privileges = object.account.privileges

        $object.path = object.path
        $object.name = object.name
        $object.fullpath = object.fullpath
        $object.property = object.property
        $object.value = object.value
        $object.new_value = object.new_value

        $event_src.host = event_src.host
        $event_src.fqdn = event_src.fqdn
        $event_src.hostname = event_src.hostname
        $event_src.ip = event_src.ip
        $event_src.asset = event_src.asset
        $event_src.vendor = event_src.vendor
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.category = event_src.category
        $event_src.rule = event_src.rule
        
        $alert.key = object.name
        $alert.content = event_src.host + "|" + object.name
    }

emit {
    $correlation_type = "incident"
    $importance = "info"

    $subject = $subject
    $action = "create"
    $object = "account"
    $status = "success"

    $category.generic = "Attack"
    $category.high = "Persistence"
    $category.low = "Create Account"

    $incident.aggregation.key = join([$correlation_name, $event_src.host], "|")
    $incident.severity = $importance
    $incident.category = "Undefined"
    $incident.aggregation.timeout = 2h
}

