event ADobject_DeleteValue:
    key:
        event_src.host, msgid, object.type, object.id
    filter {
        filter::NotFromCorrelator() and
        msgid == "5136" and 
        object == "ds_object" and
        action == "modify"  and
        in_list(["domainDNS", "19195a5b-6da0-11d0-afd3-00c04fd930c9"], object.type) == True and
        object.state == "value deleted" and
        object.property == "nTSecurityDescriptor"
    }

event ADobject_AddValue:
    key:
        event_src.host, msgid, object.type, object.id
    filter {
        filter::NotFromCorrelator() and
        msgid == "5136" and 
        object == "ds_object" and
        action == "modify"  and
        in_list(["domainDNS", "19195a5b-6da0-11d0-afd3-00c04fd930c9"], object.type) == True and
        object.state == "value added" and
        object.property == "nTSecurityDescriptor" and
        ( 
            find_substr(object.value, "1131f6ad-9c07-11d1-f79f-00c04fc2dcd2") != null or
            find_substr(object.value, "1131f6aa-9c07-11d1-f79f-00c04fc2dcd2") != null  or
            find_substr(object.value, "89e95b76-444d-4c62-991a-0facbeda640c") != null 
        )
    }


rule DCSync_prepare_Add_replicatation_rights_to_Account: ADobject_DeleteValue and  ADobject_AddValue

    init {
        $labels = "w_auto"
    }

    on ADobject_DeleteValue {
        $subject = subject
        $subject.account.name = subject.account.name
        $subject.account.domain = subject.account.domain
        $subject.account.id = subject.account.id
        $subject.account.session_id = subject.account.session_id
        $subject.account.privileges = subject.account.privileges
        
        $object = object
        $object.id = object.id
        $object.name = object.name
        $object.property = object.property
        #$object.state = object.state
        $object.type = object.type
        #$object.value = object.value

        # event src host
        $event_src.host = event_src.host
        $event_src.fqdn = event_src.fqdn
        $event_src.hostname = event_src.hostname
        $event_src.ip = event_src.ip
        $event_src.asset = event_src.asset
        $event_src.vendor = event_src.vendor
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.category = event_src.category
    }

    on ADobject_AddValue {
        $action = action

        $object.state = object.state
        $object.value = object.value
        
        $status = status
    }

emit {
    $correlation_name = "DCSync_prepare_Add_replicatation_rights_to_Account"
    $correlation_type = "incident"

    $importance = "medium"

    $category.generic = "Attack"
    $category.high = "Persistence"
    $category.low = "Account Manipulation"

    $incident.aggregation.key = join([$correlation_name, lower($event_src.host), lower($subject.account.id)], "|")
    $incident.aggregation.timeout = 2h

    $incident.severity = $importance
    
    $incident.category = "Undefined"
    $incident.type = "Undefined"
}
