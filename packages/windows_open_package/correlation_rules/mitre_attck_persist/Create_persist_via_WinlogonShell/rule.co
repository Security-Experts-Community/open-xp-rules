event RegistryModification:
    key:
        event_src.host
    filter {
        filter::NotFromCorrelator() and
        in_list(["13", "4657"], msgid) and
        lower(object) == "reg_object" and
        find_substr(lower(object.fullpath), "currentversion\\winlogon") != null and
        lower(object.name) == "shell" and
        find_substr(lower(object.new_value), ",") != null
    }

rule Create_persist_via_WinlogonShell: RegistryModification

    init {
        $labels = "w_auto"
    }

    on RegistryModification {
        $subject.account.name = subject.account.name
        $subject.account.domain = subject.account.domain
        $subject.account.id = subject.account.id
        $subject.account.session_id = subject.account.session_id
        
        $subject.process.id = subject.process.id
        $subject.process.name = subject.process.name
        $subject.process.path = subject.process.path
        $subject.process.fullpath = subject.process.fullpath

        $object.name = object.name
        $object.path = object.path
        $object.fullpath = object.fullpath
        $object.property = object.property
        $object.value = object.value
        $object.new_value = object.new_value

        $event_src.host = event_src.host
        $event_src.fqdn = event_src.fqdn
        $event_src.hostname = event_src.hostname
        $event_src.ip = event_src.ip
        $event_src.vendor = event_src.vendor
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.category = event_src.category
        $event_src.rule = event_src.rule
    }

emit {
    $correlation_name = "Create_persist_via_WinlogonShell"
    $correlation_type = "incident"

    $subject = "process"
    $action = "modify"
    $object = "reg_object"
    $status = "success"
    
    $importance = "medium"
       
    $category.generic = "Persistence"
    $category.high = "Boot or Logon Autostart Execution"
    $category.low = "Winlogon Helper DLL"

    $incident.aggregation.key = join([$correlation_name, $event_src.host, $subject.process.name], "|")
    $incident.aggregation.timeout = 2h

    $incident.severity = $importance
    $incident.category = "Undefined"
    $incident.type = "SoftwareSuspiciousActivity"
    
}
