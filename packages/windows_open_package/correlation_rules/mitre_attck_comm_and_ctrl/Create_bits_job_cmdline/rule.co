event Process_Start:
    key:
        event_src.host
    filter {
        filter::NotFromCorrelator()
        and (
                filter::ProcessStart_Windows_commandline("bitsadmin.exe", ".*transfer.*|.*create.*|.*addfile.*|.*setnotifyflags.*|.*setnotifycmdline.*|.*setminretrydelay.*|.*setcustomheaders.*|.*resume.*")      
                or
                filter::ProcessStart_Windows_commandline("powershell.exe", ".*start-bitstransfer.*|.*add-bitsfile.*|.*resume-bitstransfer.*|.*set-bitstransfer.*|.*bits.manager.*")
                or
                filter::ProcessStart_Windows_commandline("powershell_ise.exe", ".*start-bitstransfer.*|.*add-bitsfile.*|.*resume-bitstransfer.*|.*set-bitstransfer.*|.*bits.manager.*")
        )
    }


event Powershell_Pipeline_Execute:
    key:
        event_src.host
    filter {
        filter::NotFromCorrelator()
        and msgid == "4104"
        and action == "execute"
        and event_src.title == "windows"
        and object == "command"
        and event_src.subsys == "Microsoft-Windows-PowerShell/Operational"
        and (
                match(lower(object.process.cmdline),"*start-bitstransfer*")
                or
                match(lower(object.process.cmdline),"*add-bitsfile*")
                or
                match(lower(object.process.cmdline),"resume-bitstransfer")
                or
                match(lower(object.process.cmdline),"*set-bitstransfer*")
                or
                match(lower(object.process.cmdline),"*bits.manager*")
                # Командлеты Powershell для WinRM
                or match(lower(object.process.cmdline),"*wmi/root/microsoft/bits/bitsclientjob*")
                # Командлеты WMI https://learn.microsoft.com/ru-ru/windows/win32/bits/bits-compact-server
                or match(lower(object.process.cmdline),"*root\\microsoft\\bits*")
                or match(lower(object.process.cmdline),"*bitscompactserverurlgroup*")  
            )
    }

event Powershell_Command_Execute:
    key:
        event_src.host
    filter {
        filter::NotFromCorrelator()
        and msgid == "4103"
        and action == "execute"
        and event_src.title == "windows"
        and object == "command"
        and event_src.subsys == "Microsoft-Windows-PowerShell/Operational"
        and (
                match(lower(object.process.cmdline),"*start-bitstransfer*")
                or
                match(lower(object.process.cmdline),"*add-bitsfile*")
                or
                match(lower(object.process.cmdline),"resume-bitstransfer")
                or
                match(lower(object.process.cmdline),"*set-bitstransfer*")
                or
                match(lower(object.process.cmdline),"*bits.manager*")
                # Командлеты Powershell для WinRM
                or match(lower(object.process.cmdline),"*wmi/root/microsoft/bits/bitsclientjob*")
                # Командлеты WMI https://learn.microsoft.com/ru-ru/windows/win32/bits/bits-compact-server
                or match(lower(object.process.cmdline),"*root\\microsoft\\bits*")
                or match(lower(object.process.cmdline),"*bitscompactserverurlgroup*")  
            )
    }

rule Create_bits_job_cmdline: (Process_Start or Powershell_Pipeline_Execute or Powershell_Command_Execute) timer 5m

    on Process_Start {
        $action = action  # start
        $object = object  # process

        $subject.account.name = subject.account.name
        $subject.account.domain = subject.account.domain
        $subject.account.fullname = subject.account.fullname
        $subject.account.session_id = subject.account.session_id
        $subject.account.id = subject.account.id
        $subject.account.privileges = subject.account.privileges

        $object.account.session_id = object.account.session_id
        $object.account.name = object.account.name
        $object.account.domain = object.account.domain
        $object.account.fullname = object.account.fullname
        $object.account.id = object.account.id

        $object.process.id = object.process.id
        $object.process.name = object.process.name
        $object.process.path = object.process.path
        $object.process.fullpath = object.process.fullpath
        $object.process.hash = object.process.hash
        $object.process.hash.md5 = object.process.hash.md5
        $object.process.hash.sha1 = object.process.hash.sha1
        $object.process.hash.sha256 = object.process.hash.sha256
        $object.process.version = object.process.version
        $object.process.cmdline = object.process.cmdline
        $object.process.guid = object.process.guid
        $object.process.meta = object.process.meta
        $object.process.original_name = object.process.original_name
        $object.process.cwd = object.process.cwd
        $object.process.chain = object.process.chain

        $object.process.parent.id = object.process.parent.id
        $object.process.parent.name = object.process.parent.name
        $object.process.parent.path = object.process.parent.path
        $object.process.parent.fullpath = object.process.parent.fullpath
        $object.process.parent.guid = object.process.parent.guid
        $object.process.parent.cmdline = object.process.parent.cmdline

        $datafield6 = datafield6    # Идентификатор сессии в формате UUID

        $datafield18 = datafield18 # Цепочка процесса-субъекта с идентификаторами
        $datafield19 = datafield19 # Цепочка процесса-объекта с идентификаторами

        $event_src.ip = event_src.ip
        $event_src.hostname = event_src.hostname
        $event_src.fqdn = event_src.fqdn
        $event_src.host = event_src.host
        $event_src.asset = event_src.asset
        $event_src.vendor = event_src.vendor
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.rule = event_src.rule
        
    }

    on Powershell_Pipeline_Execute {
        $action = action  # execute
        $object = object  # command

        $subject.account.name = subject.account.name
        $subject.account.domain = subject.account.domain
        $subject.account.fullname = subject.account.fullname
        $subject.account.id = subject.account.id

        $object.account.name = object.account.name
        $object.account.domain = object.account.domain
        $object.account.fullname = object.account.fullname
        $object.account.id = object.account.id

        $object.process.cmdline = object.process.cmdline
        $object.process.name = object.process.name

        $object.process.parent.path = object.process.parent.path
        $object.process.parent.name = object.process.parent.name
        $object.process.parent.fullpath = object.process.parent.fullpath
        $object.process.parent.guid = object.process.parent.guid
        $object.process.parent.id = object.process.parent.id
        $object.process.parent.cmdline = object.process.parent.cmdline

        $object.name = object.name
        $object.path = object.path
        $object.fullpath = object.fullpath
        $object.value = object.value

        $datafield8 = datafield8  # версия модуля
        $datafield9 = datafield9 # Командлайн без приведения к нижнему регистру

        $chain_id = chain_id

        $event_src.host = event_src.host
        $event_src.fqdn = event_src.fqdn
        $event_src.hostname = event_src.hostname
        $event_src.ip = event_src.ip
        $event_src.asset = event_src.asset
        $event_src.vendor = event_src.vendor
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.category = event_src.category
        $event_src.rule = event_src.rule

    }

    on Powershell_Command_Execute {
        $action = action  # execute
        $object = object  # command

        $subject.account.name = subject.account.name
        $subject.account.domain = subject.account.domain
        $subject.account.fullname = subject.account.fullname
        $subject.account.id = subject.account.id

        $object.account.name = object.account.name
        $object.account.domain = object.account.domain
        $object.account.fullname = object.account.fullname
        $object.account.id = object.account.id

        $object.process.cmdline = object.process.cmdline
        $object.process.id = object.process.id

        $object.id = object.id
        $object.name = object.name
        $object.path = object.path
        $object.fullpath = object.fullpath
        $object.value = object.value

        $event_src.host = event_src.host
        $event_src.fqdn = event_src.fqdn
        $event_src.hostname = event_src.hostname
        $event_src.ip = event_src.ip
        $event_src.asset = event_src.asset
        $event_src.vendor = event_src.vendor
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.category = event_src.category
        $event_src.rule = event_src.rule

    }


emit {
    $correlation_type = "incident"
    $importance = "medium"

    $action = "alert"
    $object = "malware"
    $status = "success"

    $category.generic = "Attack"
    $category.high = "Command and Control"
    $category.low = "Ingress Tool Transfer"

    $incident.severity = "medium"
    $incident.category = "Undefined"

    $incident.aggregation.key = join([$correlation_name, $event_src.hostname, $object.process.cmdline], "|")
    $incident.severity = $importance
    $incident.aggregation.timeout = 2h
}
