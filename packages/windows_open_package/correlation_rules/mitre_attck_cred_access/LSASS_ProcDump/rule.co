event LSASS_Proccess_Access:
    key:
        event_src.host, subject.process.name
    filter {
        filter::NotFromCorrelator() and 
        event_src.title == "sysmon" and
        msgid == "10" and 
        lower(object.process.name) == "lsass.exe" and
        find_substr(lower(subject.process.name),"procdump") != null #в теории можно обойтись и без этой строчки
    }

event LSASS_Dump_Create:
    key:
        event_src.host, subject.process.name
    filter {
        filter::NotFromCorrelator() and 
        event_src.title == "sysmon" and
        msgid == "11" and 
        regex(lower(object.name), ".*lsass.*\.dmp$", 0) != null
    }

rule LSASS_ProcDump: LSASS_Proccess_Access and LSASS_Dump_Create within 10s

    on LSASS_Proccess_Access {
        $object.process.id = object.process.id
        $object.process.name = object.process.name
        $object.process.path = object.process.path
        $object.process.fullpath = object.process.fullpath
        $object.process.guid = object.process.guid
    }

    on LSASS_Dump_Create {
        $subject.process.id = subject.process.id
        $subject.process.name = subject.process.name
        $subject.process.path = subject.process.path
        $subject.process.fullpath = subject.process.fullpath
        $subject.process.guid = subject.process.guid

        $object.name = object.name
        $object.path = object.path
        $object.fullpath = object.fullpath
        $object.property = object.property
        $object.value = object.value

        $event_src.ip = event_src.ip
        $event_src.hostname = event_src.hostname
        $event_src.host = event_src.host
        $event_src.asset = event_src.asset
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.vendor = event_src.vendor
        $event_src.category = event_src.category
        $event_src.rule = event_src.rule
    }

emit {
    $correlation_type = "incident"

    $subject = "process"
    $action = "create"
    $object = "file_object"
    $status = "success"

    $importance = "high"

    $category.generic = "Attack"
    $category.high = "Credential Access"
    $category.low = "OS Credential Dumping"

    $incident.severity = $importance
    $incident.category = "SoftwareSuspiciousActivity"
}
