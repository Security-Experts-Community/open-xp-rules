event Kerberos_Pre_Auth:
    key:
        src.host
    filter {
        filter::NotFromCorrelator()
        and event_src.title == "windows"
        and event_src.subsys == "Security"
        and msgid == "4771"
        and reason == "0x18"
        and not match(subject.account.name, "*$")
    }
    
rule Kerberos_pwd_spraying: (Kerberos_Pre_Auth [10,] with different subject.account.name) timer 1h

    on Kerberos_Pre_Auth {
        $subject.account.name = subject.account.name
        $subject.account.domain = subject.account.domain
        $subject.account.fullname = subject.account.fullname
        $subject.account.session_id = subject.account.session_id
        $subject.account.id = subject.account.id
        $subject.account.privileges = subject.account.privileges
        
        $object.account.session_id = object.account.session_id
        $object.account.name = object.account.name
        $object.account.domain = object.account.domain
        $object.account.fullname = object.account.fullname
        $object.account.id = object.account.id

        $object.process.id = object.process.id
        $object.process.name = object.process.name
        $object.process.path = object.process.path
        $object.process.fullpath = object.process.fullpath
        $object.process.hash = object.process.hash
        $object.process.version = object.process.version
        $object.process.cmdline = object.process.cmdline
        $object.process.guid = object.process.guid
        $object.process.meta = object.process.meta
        $object.process.original_name = object.process.original_name
        $object.process.cwd = object.process.cwd
        
        $object.query = object.query
        $object.property = object.property
        $object.value = object.value

        $src.host = src.host
        $src.fqdn = src.fqdn
        $src.hostname = src.hostname
        $src.ip = src.ip
        $src.port = src.port
        $src.asset = src.asset

        $event_src.host = event_src.host
        $event_src.fqdn = event_src.fqdn
        $event_src.hostname = event_src.hostname
        $event_src.ip = event_src.ip
        $event_src.asset = event_src.asset
        $event_src.vendor = event_src.vendor
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.category = event_src.category
        $event_src.rule = event_src.rule
    }

emit {
    $correlation_type = "incident"

    $subject = "account"
    $action = "execute"
    $object = "request"
    $status = "success"

    $importance = "low"

    $category.generic = "Attack"
    $category.high = "Credential Access"
    $category.low = "Brute Force: Password Spraying"

    $incident.aggregation.key = join([$correlation_name, lower($src.host)], "|")
    $incident.severity = $importance
    $incident.category = "UserCompromising"
    $incident.aggregation.timeout = 1h
}