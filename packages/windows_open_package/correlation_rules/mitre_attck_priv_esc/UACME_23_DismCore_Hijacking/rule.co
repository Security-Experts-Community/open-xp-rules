event Start_Dism_By_Pkgmgr_Oemsetup:
    key:
        event_src.host
    filter {
        filter::NotFromCorrelator()
        and filter::ProcessStart_Windows_any()
        and match(lower(object.process.fullpath), "?:\\windows\\system32\\dism.exe")
        and match(lower(object.process.parent.fullpath), "?:\\windows\\system32\\pkgmgr.exe")
        and match(lower(object.process.parent.cmdline), "*oemsetup.xml")
    }

event Start_Process_By_Dism:
    key:
        event_src.host
    filter {
        filter::NotFromCorrelator()
        and filter::ProcessStart_Windows_any()
        and match(lower(object.process.parent.fullpath), "?:\\windows\\system32\\dism.exe")
    }

rule UACME_23_DismCore_Hijacking: (Start_Dism_By_Pkgmgr_Oemsetup and Start_Process_By_Dism) within 10s
    on Start_Dism_By_Pkgmgr_Oemsetup {
        $datafield2 = object.process.parent.cmdline
    }

    on Start_Process_By_Dism {
        $event_src.ip = event_src.ip
        $event_src.hostname = event_src.hostname
        $event_src.host = event_src.host
        $event_src.asset = event_src.asset
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.vendor = event_src.vendor
        $event_src.category = event_src.category
        $event_src.rule = event_src.rule

        $subject.account.name = subject.account.name
        $subject.account.fullname = subject.account.fullname
        $subject.account.domain = subject.account.domain
        $subject.account.session_id = subject.account.session_id
        $subject.account.id = subject.account.id
        $subject.account.privileges = subject.account.privileges

        $object.account.name = object.account.name
        $object.account.fullname = object.account.fullname
        $object.account.domain = object.account.domain
        $object.account.session_id = object.account.session_id
        $object.account.id = object.account.id
        $object.account.privileges = object.account.privileges

        $object.process.id = object.process.id
        $object.process.name = object.process.name
        $object.process.path = object.process.path
        $object.process.fullpath = object.process.fullpath
        $object.process.hash = object.process.hash
        $object.process.hash.md5 = object.process.hash.md5
        $object.process.hash.sha1 = object.process.hash.sha1
        $object.process.hash.sha256 = object.process.hash.sha256
        $object.process.version = object.process.version
        $object.process.cmdline = object.process.cmdline
        $object.process.guid = object.process.guid
        $object.process.meta = object.process.meta
        $object.process.original_name = object.process.original_name
        $object.process.cwd = object.process.cwd
        $object.process.chain = object.process.chain
        
        $object.process.parent.id = object.process.parent.id
        $object.process.parent.name = object.process.parent.name
        $object.process.parent.path = object.process.parent.path
        $object.process.parent.fullpath = object.process.parent.fullpath
        $object.process.parent.cmdline = object.process.parent.cmdline
        
        $datafield1 = object.process.name + " ← dism.exe ← pkgmgr.exe"
    }

emit {
    $correlation_type = "incident"

    $status = "success"
    $subject = "account"
    $action = "escalate"
    $object = "process"

    $importance = "medium"

    $category.generic = "Attack"
    $category.high = "Privilege Escalation"
    $category.low = "Bypass User Account Control"

    $incident.aggregation.key = join([$correlation_name, $event_src.host, $object.process.cmdline], "|")
    $incident.severity = $importance
    $incident.category = "Undefined"
    $incident.aggregation.timeout = 10m
}
