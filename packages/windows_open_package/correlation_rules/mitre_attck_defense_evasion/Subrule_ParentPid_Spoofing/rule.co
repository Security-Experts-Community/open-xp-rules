event AccessProccess:
    key:
        event_src.host, subject.process.id, subject.process.fullpath
    filter {
        filter::NotFromCorrelator()
        and msgid == "10"
        and event_src.title == "sysmon"
        and in_list(["0x1fffff", "0x1f3fff"], object.value)
    }

rule Subrule_ParentPid_Spoofing: AccessProccess[2] with different object.process.id within 5s
    init {
        $first_event = true
    }

    on AccessProccess {
        # Маска доступа для родительского процесса
        if object.value == "0x1f3fff" then
            $object.process.parent.name = object.process.name
            $object.process.parent.id = object.process.id
            $object.process.parent.fullpath = object.process.fullpath
            $object.process.parent.path = object.process.path
        # Маска доступа для дочернего процесса
        elif object.value == "0x1fffff" then
            $object.process.path = object.process.path
            $object.process.name = object.process.name
            $object.process.fullpath = object.process.fullpath
            $object.process.id = object.process.id
        endif

        if $first_event then 
            $subject.account.name = subject.account.name
            $subject.account.domain = subject.account.domain
            $subject.account.fullname = subject.account.fullname
            $subject.account.id = subject.account.id
            $subject.account.session_id = subject.account.session_id

            $subject.process.id = subject.process.id
            $subject.process.name = subject.process.name
            $subject.process.path = subject.process.path
            $subject.process.fullpath = subject.process.fullpath
            $subject.process.guid = subject.process.guid
            
            $object.property = object.property
            $object.value = object.value

            $object.account.id = object.account.id
            $object.account.name = object.account.name
            $object.account.domain = object.account.domain
            $object.account.session_id = object.account.session_id
            $object.account.privileges = object.account.privileges

            $object.process.guid = object.process.guid

            $datafield5 = datafield5            # Идентификатор потока субъекта
            $datafield9 = datafield9            # Цепочка вызовов процесса

            $event_src.host = event_src.host
            $event_src.fqdn = event_src.fqdn
            $event_src.hostname = event_src.hostname
            $event_src.ip = event_src.ip
            $event_src.asset = event_src.asset
            $event_src.vendor = event_src.vendor
            $event_src.title = event_src.title
            $event_src.subsys = event_src.subsys
            $event_src.category = event_src.category
            $event_src.rule = event_src.rule

            $first_event = false
        else
            $object.value = $object.value + "|" + object.value
        endif
    }

emit {
    $correlation_type = "subrule"

    $subject = "process"
    $action = "access"
    $object = "process"
    $status = "success"

    $importance = "info"

    $category.generic = "Attack"
    $category.high = "Defense Evasion"
    $category.low = "Access Token Manipulation: Parent PID Spoofing"
}
