event Process_Start:
    key:
        event_src.host, object.process.id
    filter {
        filter::NotFromCorrelator() and
        in_list(["1","4688"], msgid) and
        lower(object) == "process" and
        lower(action) == "start"
    }
    
event AccessProccess:
    key:
        event_src.host, object.process.id
    filter {
        filter::NotFromCorrelator() and 
        msgid == "10" and
        lower(object) == "process" and
        lower(action) == "access" and 
        in_list(["0x1fffff", "0x0028", "0x043A"], object.value) != null
        # PROCESS_VM_WRITE (0x0020) + PROCESS_VM_OPERATION (0x0008) = 
        # PROCESS_CREATE_THREAD (0x0002) + PROCESS_QUERY_INFORMATION (0x0400) + PROCESS_VM_OPERATION (0x0008) + PROCESS_VM_WRITE (0x0020) + PROCESS_VM_READ (0x0010) = 0x043A
        # PROCESS_ALL_ACCESS = 0x1FFFFF
    }
event Network_Connect:
    key:
        event_src.host, object.process.id
    filter {
        filter::NotFromCorrelator() and
        in_list(["5156","3"], msgid) and
        lower(object) == "connection" and
        lower(action) == "detect"
    }

rule ReverseShell_created_via_PEInjection: Process_Start and AccessProccess and Network_Connect

    init {
        $labels = "w_auto" 
    }

    on Process_Start {
        $subject.account.name = subject.account.name
        $subject.account.domain = subject.account.domain
        $subject.account.fullname = subject.account.fullname
        $subject.account.session_id = subject.account.session_id
        $subject.account.id = subject.account.id
        $subject.account.privileges = subject.account.privileges

        $object.account.name = object.account.name
        $object.account.domain = object.account.domain
        $object.account.fullname = object.account.fullname
        $object.account.session_id = object.account.session_id
        $object.account.id = object.account.id

        $object.process.id = object.process.id
        $object.process.name = object.process.name
        $object.process.path = object.process.path
        $object.process.fullpath = object.process.fullpath
        $object.process.hash = object.process.hash
        $object.process.hash.md5 = object.process.hash.md5
        $object.process.hash.sha1 = object.process.hash.sha1
        $object.process.hash.sha256 = object.process.hash.sha256
        $object.process.version = object.process.version
        $object.process.cmdline = object.process.cmdline
        $object.process.meta = object.process.meta
        $object.process.original_name = object.process.original_name
        $object.process.cwd = object.process.cwd
        $object.process.chain = object.process.chain

        $object.process.parent.id = object.process.parent.id
        $object.process.parent.name = object.process.parent.name
        $object.process.parent.path = object.process.parent.path
        $object.process.parent.fullpath = object.process.parent.fullpath
        $object.process.parent.cmdline = object.process.parent.cmdline

        $event_src.ip = event_src.ip
        $event_src.hostname = event_src.hostname
        $event_src.fqdn = event_src.fqdn
        $event_src.host = event_src.host
        $event_src.asset = event_src.asset
        $event_src.vendor = event_src.vendor
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.rule = event_src.rule
    }
    
    on AccessProccess {       
        $object.property = object.property
        $object.value = object.value

        $datafield5 = datafield5            # Идентификатор потока субъекта
        $datafield9 = datafield9            # Цепочка вызовов процесса

    }
    on Network_Connect {
        $direction = direction

        $src.host = src.host
        $src.fqdn = src.fqdn
        $src.hostname = src.hostname
        $src.ip = src.ip
        $src.port = src.port
        $src.asset = src.asset

        $protocol = protocol
        
        $dst.host = dst.host
        $dst.fqdn = dst.fqdn
        $dst.hostname = dst.hostname
        $dst.ip = dst.ip
        $dst.port = dst.port
        $dst.asset = dst.asset
    }
emit {
    $correlation_name = "ReverseShell_created_via_PEInjection"
    $correlation_type = "incident"

    $subject = "process"
    $action = "access"
    $object = "process"
    $status = "success"

    $importance = "high"

    $category.generic = "Attack"
    $category.high = "Defense Evasion"
    $category.low = "Reflective Code Loading"

    $incident.aggregation.key = join([$correlation_name, $event_src.host, $object.process.id], "|")
    $incident.aggregation.timeout = 2h
    
    $incident.severity = $importance

    $incident.category = "Undefined"
    $incident.type = "SoftwareSuspiciousActivity"
}
