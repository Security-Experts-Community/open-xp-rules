event Clear_event:
    key:
        event_src.host
    filter {
        filter::NotFromCorrelator()

        and event_src.title == "windows"
        and action == "clean"
        and object == "log"

        and 
        (
                (
                    lower(event_src.subsys) == "system"         # system, application, setup, etc 
                    and msgid == "104"
                )
            or 
                (
                    lower(event_src.subsys) == "security"     # security
                    and msgid == "1102"
                )
        )
    }

rule Clearing_eventlog: Clear_event

    init {
        $labels = "w_auto|CheckWL_Tasks"
    }

    on Clear_event {
        $action = action

        $subject.account.id = subject.account.id
        $subject.account.name = subject.account.name
        $subject.account.domain = subject.account.domain
        $subject.account.fullname = subject.account.fullname
        $subject.account.session_id = datafield1

        $subject.process.id = subject.process.id
        $subject.process.parent.id = subject.process.parent.id
        $subject.process.guid = subject.process.guid

        $object.name = object.name
        $object.type = object.type
        $object.property = object.property
        $object.value = object.value    # Path to backup logs, without Security
        $object.vendor = object.vendor
        $object.state = object.state

        $datafield4 = datafield4

        $event_src.host = event_src.host
        $event_src.fqdn = event_src.fqdn
        $event_src.hostname = event_src.hostname
        $event_src.ip = event_src.ip
        $event_src.asset = event_src.asset
        $event_src.vendor = event_src.vendor
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.category = event_src.category
        $event_src.rule = event_src.rule

        $alert.key = $object.name
        $alert.context = subject.account.domain + "\\" + $subject.account.name + " " + $action + " " + $object.name
    }

emit {
    $correlation_type = "incident"

    $subject = "account"
    $action = "clean"
    $object = "log"
    $status = "success"

    $importance = "medium"

    $category.generic = "Attack"
    $category.high = "Defense Evasion"
    $category.low = "Indicator Removal"

    $incident.aggregation.key = join([$correlation_name, $event_src.host], "|")
    $incident.severity = "medium"
    $incident.category = "Undefined"
    $incident.aggregation.timeout = 2h
}
