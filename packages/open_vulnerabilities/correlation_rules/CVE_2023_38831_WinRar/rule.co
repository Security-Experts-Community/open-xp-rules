query CheckExtension($cmdline) from Script_Extensions {
    regex_match($cmdline, column::extension)
}

event File_Created:
    key:
        event_src.host, subject.process.id
    filter {
        filter::NotFromCorrelator()
        and filter::FileCreate_Windows()
        and lower(subject.process.name) == "winrar.exe"
        and (
            regex(lower(object.name), "\.\S{2,6}\s\.exe\.?$", 0) != null
            or (
                select_query_first("CheckExtension", [lower(object.name)], "extension") != null
                and regex(lower(object.name), "\.\S{2,6}\s" + select_query_first("CheckExtension", [lower(object.name)], "extension") + "\.?$", 0) != null
            )
        )
    }

event Process_Start:
    key:
        event_src.host, object.process.parent.id
    filter {
        filter::NotFromCorrelator()
        and filter::ProcessStart_Windows_any()
        and lower(object.process.parent.name) == "winrar.exe"
        and (
            regex(lower(object.process.cmdline), "\.\S{2,6}\s\.exe", 0) != null
            or (
                select_query_first("CheckExtension", [lower(object.process.cmdline)], "extension") != null
                and regex(lower(object.process.cmdline), "\.\S{2,6}\s" + select_query_first("CheckExtension", [lower(object.process.cmdline)], "extension"), 0) != null
            )
        )
    }

rule CVE_2023_38831_WinRar: (Process_Start and File_Created?) timer 5s

    on File_Created {
        $subject.process.id = subject.process.id
        $subject.process.name = subject.process.name
        $subject.process.path = subject.process.path
        $subject.process.fullpath = subject.process.fullpath
        $subject.process.guid = subject.process.guid

        $object.name = object.name
        $object.path = object.path
        $object.fullpath = object.fullpath
        $object.property = object.property
        $object.value = object.value

        $alert.context = object.name
    }

    on Process_Start {
        $subject.account.name = subject.account.name
        $subject.account.domain = subject.account.domain
        $subject.account.fullname = subject.account.fullname
        $subject.account.session_id = subject.account.session_id
        $subject.account.id = subject.account.id
        $subject.account.privileges = subject.account.privileges

        $object.account.session_id = object.account.session_id
        $object.account.name = object.account.name
        $object.account.domain = object.account.domain
        $object.account.fullname = object.account.fullname
        $object.account.id = object.account.id

        $object.process.id = object.process.id
        $object.process.name = object.process.name
        $object.process.path = object.process.path
        $object.process.fullpath = object.process.fullpath
        $object.process.hash = object.process.hash
        $object.process.hash.md5 = object.process.hash.md5
        $object.process.hash.sha1 = object.process.hash.sha1
        $object.process.hash.sha256 = object.process.hash.sha256
        $object.process.version = object.process.version
        $object.process.cmdline = object.process.cmdline
        $object.process.guid = object.process.guid
        $object.process.meta = object.process.meta
        $object.process.original_name = object.process.original_name
        $object.process.cwd = object.process.cwd
        $object.process.chain = object.process.chain

        $object.process.parent.id = object.process.parent.id
        $object.process.parent.name = object.process.parent.name
        $object.process.parent.path = object.process.parent.path
        $object.process.parent.fullpath = object.process.parent.fullpath
        $object.process.parent.guid = object.process.parent.guid
        $object.process.parent.cmdline = object.process.parent.cmdline
        
        $datafield6 = datafield6    # Идентификатор сессии в формате UUID

        $datafield18 = datafield18 # Цепочка процесса-субъекта с идентификаторами
        $datafield19 = datafield19 # Цепочка процесса-объекта с идентификаторами

        $detect = "CVE-2023-38831"

        $event_src.ip = event_src.ip
        $event_src.hostname = event_src.hostname
        $event_src.fqdn = event_src.fqdn
        $event_src.host = event_src.host
        $event_src.asset = event_src.asset
        $event_src.vendor = event_src.vendor
        $event_src.title = event_src.title
        $event_src.subsys = event_src.subsys
        $event_src.rule = event_src.rule
        
        $alert.regex_match = regex(lower(object.process.cmdline), "\.\S{2,6}\s+\.[a-zA-Z0-9]{2,6}", 0)
        $alert.key = lower(join([subject.account.name,  object.process.parent.name, object.process.fullpath], "|"))
    }

emit {
    $correlation_type = "incident"

    $subject = "account"
    $action = "start"
    $object = "process"
    $status = "success"

    $importance = "high"

    $category.generic = "Attack"
    $category.high = "Defense Evasion"
    $category.low = "Masquerading: Space after Filename"

    $incident.aggregation.key = join([$correlation_name, lower($event_src.host), lower($subject.account.id)], "|")
    $incident.severity = $importance
    $incident.category = "Undefined"
    $incident.aggregation.timeout = 2h
}
