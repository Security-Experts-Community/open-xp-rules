EVENTLOG = 'EventID.text="33205"'

COND = (
        find_substr($Provider["Name"], "MSSQL$") != null
        or $Provider["Name"] == 'MSSQLSERVER'
       )
       and (
            find_substr($Data[0], "action_id:LGIS") != null
            or find_substr($Data[0], "action_id:LGIF") != null
           )
       and find_substr($Data[0], "class_type:LX") != null

$kv = replace($Data[0], " ", "\r\n")
$kv = keyvalue($kv, "\n", ":")

subject = "account"
action = "connect"
object = "database"

if $kv['succeeded'] == "true" then
    status = "success"
else
    status = "failure"
endif

# разбираем домен и имя пользователя
$csv = csv($kv['server_principal_name'], "\\", "'")
if length($csv) == 2 then
    subject.account.domain = lower($csv[0])
    subject.account.name = lower($csv[1])
else
    subject.account.name = lower($csv[0])
endif

subject.account.id = $kv['server_principal_sid']

src.ip = $kv['client_ip']

datafield2 = $kv['server_instance_name']    # имя экземпляра
datafield4 = $kv['action_id']               # идентификатор действия
datafield5 = $kv['class_type']              # тип класса сущности, для которой был проведен аудит
datafield8 = $kv['application_name']        # название приложения клиента
datafield9 = $kv['additional_information']  # дополнительная информация о подключении

if status == "failure" then
    reason = $kv['statement']
endif

time = $kv['event_time']
msgid = $EventID['text']
importance = "high"

event_src.vendor = "microsoft"
event_src.title = "sql_server"

$first_dot = find_substr($Computer, '.')
if $first_dot != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, $first_dot))
    dst.fqdn = event_src.fqdn
    dst.hostname = event_src.hostname
else
    event_src.hostname = lower($Computer)
    dst.hostname = event_src.hostname
endif
event_src.subsys = $Channel
event_src.category = "Database server"

id = "PT_Microsoft_SQL_Server_eventlog_33205_connection"

# -- DEPRECATED --
# lines from here will be deleted in future releases

subject.name = subject.account.name
subject.domain = subject.account.domain
subject.id = subject.account.id