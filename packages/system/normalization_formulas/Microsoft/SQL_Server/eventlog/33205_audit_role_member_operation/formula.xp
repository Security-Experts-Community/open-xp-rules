EVENTLOG = 'EventID.text="33205"'

COND = (
        find_substr($Provider["Name"], "MSSQL$") != null
        or $Provider["Name"] == 'MSSQLSERVER'
       )
       and (
            find_substr($Data[0], "action_id:APRL") != null
            or find_substr($Data[0], "action_id:DPRL") != null
           )
       and (
            find_substr($Data[0], "class_type:SG") != null
            or find_substr($Data[0], "class_type:RL") != null
           )

$kv = replace($Data[0], " ", "\r\n")
$kv = keyvalue($kv, "\n", ":")

subject = "account"

action = switch $kv['action_id']
    case "APRL" "grant"
    case "DPRL" "revoke"
endswitch

object = "db_object"

if $kv['succeeded'] == "true" then
    status = "success"
else
    status = "failure"
endif

# разбираем домен и имя пользователя
$csv = csv($kv['server_principal_name'], "\\", "'")
if length($csv) == 2 then
    subject.account.domain = lower($csv[0])
    subject.account.name = lower($csv[1])
else
    subject.account.name = lower($csv[0])
endif

subject.account.id = $kv['server_principal_sid']

object.name = $kv['object_name']

object.type = "role"
object.property = "affected entity"

object.value = switch $kv['class_type']
    case "RL" $kv['target_database_principal_name']
    case "SG" $kv['target_server_principal_name']
endswitch

object.query = $kv['statement']

datafield2 = $kv['server_instance_name']  # имя экземпляра
datafield3 = $kv['database_name']  # имя используемой БД
datafield4 = $kv['action_id']  # идентификатор действия
datafield5 = $kv['class_type']  # Тип класса сущности, для которой был проведен аудит

time = $kv['event_time']
msgid = $EventID['text']
importance = "medium"

event_src.vendor = "microsoft"
event_src.title = "sql_server"

$first_dot = find_substr($Computer, '.')
if $first_dot != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, $first_dot))
else
    event_src.hostname = lower($Computer)
endif
event_src.subsys = $Channel
event_src.category = "Database server"

id = "PT_Microsoft_SQL_Server_eventlog_33205_audit_role_member_operation"

# -- DEPRECATED --
# lines from here will be deleted in future releases

subject.name = subject.account.name
subject.domain = subject.account.domain
subject.id = subject.account.id

datafield1 = object.query  # текст выполняемого запроса без переносов строки