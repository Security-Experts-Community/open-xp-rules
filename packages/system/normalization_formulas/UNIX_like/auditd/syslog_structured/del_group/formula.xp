JSON = ''
COND = (
    $eventid != null
    and $node != null
    and $timestamp != null
    and $items["DEL_GROUP"] != null
)

$kv_main = keyvalue($items["DEL_GROUP"][0], " ", "=", "'")
$kv_msg = keyvalue($kv_main["msg"], " ", "=", '"')

if $kv_main["auid"] != "4294967295" then
    subject = "account"
endif

action = "remove"
object = "user_group"

if $kv_msg["exe"] != "?" then
    # Декодируем HEX, если требуется
    $exe = strip($kv_msg["exe"], '"', '"')
    if $exe == $kv_msg["exe"] then
        $exe = hex_to_string($kv_msg["exe"])
    endif
    
    $process_path_and_name = csv($exe, "/", "")
    $process_fullpath = $exe
    $process_name = $process_path_and_name[length($process_path_and_name) - 1]
    $process_path = strip($exe, "", $process_name)
endif

# groupdel
if ($process_name == "groupdel") and ($kv_msg["op"] == null) then
    status = "success"
elif ($process_name == "groupdel") and ($kv_msg["res"] == "success") then
    status = "ongoing"

# userdel
elif ($process_name == "userdel") and ($kv_msg["res"] == "success") then
    status = "success"

# groupdel & userdel
else
    status = "failure"
endif

# Декодируем HEX, если требуется
$auid_en = strip($kv_main["AUID"], '"', '"')
if $auid_en == $kv_main["AUID"] then
    $auid_en = hex_to_string($kv_main["AUID"])
endif

$auid_list = csv($auid_en, "@", "")

# Декодируем HEX, если требуется
$uid_en = strip($kv_main["UID"], '"', '"')
if $uid_en == $kv_main["UID"] then
    $uid_en = hex_to_string($kv_main["UID"])
endif

$uid_list = csv($uid_en, "@", "")

if $kv_main["auid"] != "4294967295" then
    subject.account.name = $auid_list[0]
    subject.account.domain = $auid_list[1]

    subject.account.id = $kv_main["auid"]
else
    subject.account.name = $uid_list[0]
    subject.account.domain = $uid_list[1]

    subject.account.id = $kv_main["uid"]
endif

subject.account.session_id = $kv_main["ses"]
subject.account.privileges = $kv_main["uid"]

subject.process.fullpath = $process_fullpath
subject.process.name = $process_name
subject.process.path = $process_path
subject.process.id = $kv_main["pid"]

# Декодируем HEX, если требуется
$id_en = strip($kv_main["ID"], '"', '"')
if $id_en == $kv_main["ID"] then
    $id_en = hex_to_string($kv_main["ID"])
endif

# Декодируем HEX, если требуется
$acct = strip($kv_msg["acct"], '"', '"')
if $acct == $kv_msg["acct"] then
    $acct = hex_to_string($kv_msg["acct"])
endif

if ($kv_msg["id"] != null) then
    object.id = $kv_msg["id"]
    object.name = $id_en
else
    object.name = $acct
endif

object.state = $kv_msg["op"]

if $kv_msg["terminal"] != "?" then
    logon_service = strip($kv_msg["terminal"], "/dev/", "")
endif

chain_id = $eventid  # AuditD Event ID

category.generic = "Group"
category.high = "Users And Rights Management"
category.low = "Manipulation"

time = epoch_to_datetime(number($timestamp))
msgid = "DEL_GROUP"
importance = "low"

event_src.title = "unix_like"
event_src.subsys = "auditd"
event_src.category = "Operating system"

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
        else
            event_src.ip = recv_ipv4
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
        else
            event_src.hostname = $node
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

if (event_src.hostname == null) and not in_list([$kv_msg["addr"], "?"], $kv_msg["hostname"]) then
    event_src.hostname = $kv_msg["hostname"]
endif

if (event_src.ip == null) and ($kv_msg["addr"] != "?") then
    event_src.ip = $kv_msg["addr"]
endif

id = "PT_UNIX_like_auditd_syslog_structured_del_group"
