JSON = ''
COND = (
    $eventid != null
    and $node != null
    and $timestamp != null
    and $items["GRP_MGMT"] != null
)

$kv_main = keyvalue($items["GRP_MGMT"][0], " ", "=", "'")
$kv_msg = keyvalue($kv_main["msg"], " ", "=", '"')

if $kv_main["auid"] != "4294967295" then
    subject = "account"
endif

if $kv_msg["exe"] != "?" then
    # Декодируем HEX, если требуется
    $exe = strip($kv_msg["exe"], '"', '"')
    if $exe == $kv_msg["exe"] then
        $exe = hex_to_string($kv_msg["exe"])
    endif
    
    $process_path_and_name = csv($exe, "/", "")
    $process_fullpath = $exe
    $process_name = $process_path_and_name[length($process_path_and_name) - 1]
    $process_path = strip($exe, "", $process_name)
endif

# gpasswd
if $process_name == "gpasswd" then
    action = "modify"
    status = if $kv_msg["res"] == "success" then "success" else "failure" endif

# groupadd
elif $process_name == "groupadd" then
    action = "create"
    status = "ongoing"

# groupdel
elif $process_name == "groupdel" then
    action = "remove"
    status = if $kv_msg["res"] == "success" then "ongoing" else "failure" endif

# groupmod
elif $process_name == "groupmod" then
    action = "modify"
    
    if ($kv_msg["res"] == "success") and find_substr($kv_msg["op"], "changing-") != null then
        status = "ongoing"
    elif $kv_msg["res"] == "success"  then
        status = "success"
    else
        status = "failure"
    endif

# userdel
elif $process_name == "userdel" then
    action = "remove"
    status = "success"

# usermod
elif $process_name == "usermod" then
    action = "modify"
    status = "success"
endif

object = "user_group"

# Декодируем HEX, если требуется
$auid_en = strip($kv_main["AUID"], '"', '"')
if $auid_en == $kv_main["AUID"] then
    $auid_en = hex_to_string($kv_main["AUID"])
endif

$auid_list = csv($auid_en, "@", "")

# Декодируем HEX, если требуется
$uid_en = strip($kv_main["UID"], '"', '"')
if $uid_en == $kv_main["UID"] then
    $uid_en = hex_to_string($kv_main["UID"])
endif

$uid_list = csv($uid_en, "@", "")

if $kv_main["auid"] != "4294967295" then
    subject.account.name = $auid_list[0]
    subject.account.domain = $auid_list[1]

    subject.account.id = $kv_main["auid"]
else
    subject.account.name = $uid_list[0]
    subject.account.domain = $uid_list[1]

    subject.account.id = $kv_main["uid"]
endif

subject.account.session_id = $kv_main["ses"]
subject.account.privileges = $kv_main["uid"]

subject.process.fullpath = $process_fullpath
subject.process.name = $process_name
subject.process.path = $process_path
subject.process.id = $kv_main["pid"]

$property = ""
$value = ""
$new_value = ""

if ($process_name == "groupmod") and (find_substr($kv_msg["op"], "changing") != null) then
    if $kv_msg["new_group"] != null then
        $property = $property + " | name"
        $value = $value + " | " + $kv_msg["grp"]
        $new_value = $new_value + " | " + $kv_msg["new_group"]
    endif
    
    if $kv_msg["new_gid"] != null then
        $property = $property + " | id"
        $value = $value + " | " + $kv_msg["gid"]
        $new_value = $new_value + " | " + $kv_msg["new_gid"]
    endif

    object.name = $old_name
    object.id = $old_gid

    object.property = join($key_list, '|')
    object.value = join($list, '|')
endif

# Декодируем HEX, если требуется
$id_en = strip($kv_main["ID"], '"', '"')
if $id_en == $kv_main["ID"] then
    $id_en = hex_to_string($kv_main["ID"])
endif

# Декодируем HEX, если требуется
$acct = strip($kv_msg["acct"], '"', '"')
if $acct == $kv_msg["acct"] then
    $acct = hex_to_string($kv_msg["acct"])
endif

if ($process_name == "gpasswd") and ($kv_msg["op"] == "set-admins-of-group") then
    $property = "group_admins"
    $new_value = $acct
endif

if in_list(["groupadd", "groupdel"], $process_name) then
    object.name = coalesce($id_en, $acct)
    object.id = $kv_msg["id"]
else
    object.name = $kv_msg["grp"]
    object.id = $kv_msg["gid"]
endif

object.property = if $property != "" then strip($property, " | ", "") else null endif
object.value = if $value != "" then strip($value, " | ", "") else null endif
object.new_value = if $new_value != "" then strip($new_value, " | ", "") else null endif

object.state = $kv_msg["op"]

if $kv_msg["terminal"] != "?" then
    logon_service = strip($kv_msg["terminal"], "/dev/", "")
endif

chain_id = $eventid  # AuditD Event ID

time = epoch_to_datetime(number($timestamp))
msgid = "GRP_MGMT"
importance = "low"

category.generic = "Group"
category.high = "Users And Rights Management"
category.low = "Manipulation"

event_src.title = "unix_like"
event_src.subsys = "auditd"
event_src.category = "Operating system"

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
        else
            event_src.ip = recv_ipv4
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
        else
            event_src.hostname = $node
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

if (event_src.hostname == null) and not in_list([$kv_msg["addr"], "?"], $kv_msg["hostname"]) then
    event_src.hostname = $kv_msg["hostname"]
endif

if (event_src.ip == null) and ($kv_msg["addr"] != "?") then
    event_src.ip = $kv_msg["addr"]
endif

id = "PT_UNIX_like_auditd_syslog_structured_grp_mgmt"
