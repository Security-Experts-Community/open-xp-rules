JSON = ''
COND = (
    $eventid != null
    and $node != null
    and $timestamp != null
    and (
        (
            find_substr($items["SYSCALL"][0], "arch=c000003e") != null
            and (
                find_substr($items["SYSCALL"][0], "syscall=313") != null     # finit_module
                or find_substr($items["SYSCALL"][0], "syscall=175") != null  # init_module
                or find_substr($items["SYSCALL"][0], "syscall=176") != null  # delete_module
            )
        )
        or (
            find_substr($items["SYSCALL"][0], "arch=40000003") != null
            and (
                find_substr($items["SYSCALL"][0], "syscall=350") != null     # finit_module
                or find_substr($items["SYSCALL"][0], "syscall=128") != null  # init_module
                or find_substr($items["SYSCALL"][0], "syscall=129") != null  # delete_module
            )
        )
    )
)

$kv_syscall = keyvalue($items['SYSCALL'][0], ' ', '=')

if $kv_syscall["auid"] != "4294967295" then
    subject = "account"
endif

# Декодируем HEX, если требуется
$comm = strip($kv_syscall["comm"], '"', '"')
if $comm == $kv_syscall["comm"] then
    $comm = hex_to_string($kv_syscall["comm"])
endif

if $comm == "modprobe" then
    action = "start"
elif $comm == "rmmod" then
    action = "stop"
else
    action = "modify"
endif

object = "module"

if $kv_syscall["success"] == "yes" then
    status = "success"
else
    status = "failure"
endif

# Декодируем HEX, если требуется
$auid_en = strip($kv_syscall["AUID"], '"', '"')
if $auid_en == $kv_syscall["AUID"] then
    $auid_en = hex_to_string($kv_syscall["AUID"])
endif

$auid_list = csv($auid_en, "@", "")

# Декодируем HEX, если требуется
$euid_en = strip($kv_syscall["EUID"], '"', '"')
if $euid_en == $kv_syscall["EUID"] then
    $euid_en = hex_to_string($kv_syscall["EUID"])
endif

$euid_list = csv($euid_en, "@", "")

if $kv_syscall["auid"] != "4294967295" then
    subject.account.name = $auid_list[0]
    subject.account.domain = $auid_list[1]

    subject.account.id = $kv_syscall["auid"]
else
    subject.account.name = $euid_list[0]
    subject.account.domain = $euid_list[1]

    subject.account.id = $kv_syscall["euid"]
endif

subject.account.privileges = $kv_syscall["euid"]
subject.account.session_id = $kv_syscall["ses"]

subject.process.id = $kv_syscall["pid"]
subject.process.parent.id = $kv_syscall["ppid"]

# SYSCALL::exe
if not in_list(["<no_memory>", "<too_long>", "(null)"], $kv_syscall["exe"]) then
    # Декодируем HEX, если требуется
    $exe = strip($kv_syscall["exe"], '"', '"')
    if $exe == $kv_syscall["exe"] then
        $exe = hex_to_string($kv_syscall["exe"])
    endif
endif

$process_path_and_name = csv($exe, "/", "")
$process_fullpath = $exe
$process_name = $process_path_and_name[length($process_path_and_name) - 1]
$process_path = strip($exe, "", $process_name)

subject.process.fullpath = $process_fullpath
subject.process.name = $process_name
subject.process.path = $process_path

# PROCTITLE
if $items["PROCTITLE"] != null then
    $kv_proctitle = keyvalue($items["PROCTITLE"][0], ' ', '=')
 
    $process_proctitle = strip($kv_proctitle["proctitle"], '"', '"')
    if $process_proctitle == "(null)" then
        $process_proctitle = null
    elif $process_proctitle == $kv_proctitle["proctitle"] then
        $cmdline_raw = hex_to_string($kv_proctitle["proctitle"])
 
        # stripping corrupted char
        if $cmdline_raw == $kv_proctitle["proctitle"] then
            $cmdline_raw = hex_to_string(substr($kv_proctitle["proctitle"], 0, 254))
        endif
        if $cmdline_raw == $kv_proctitle["proctitle"] then
            $cmdline_raw = hex_to_string(substr($kv_proctitle["proctitle"], 0, 253))
        endif
        if $cmdline_raw == $kv_proctitle["proctitle"] then
            $cmdline_raw = hex_to_string(substr($kv_proctitle["proctitle"], 0, 252))
        endif
 
        $process_proctitle = replace($cmdline_raw, " ", "\u0000")
    endif
endif

subject.process.meta = $process_proctitle  # PROCTITLE

# Декодируем HEX, если требуется
$egid_en = strip($kv_syscall["EGID"], '"', '"')
if $egid_en == $kv_syscall["EGID"] then
    $egid_en = hex_to_string($kv_syscall["EGID"])
endif

object.account.group = coalesce($egid_en, $kv_syscall["egid"])

object.account.name = $euid_list[0]
object.account.domain = $euid_list[1]

object.account.id = $kv_syscall["euid"]

object.type = "executable_module"
object.state = "kernel_module_change"

# KERN_MODULE
$kv_kern_mod = keyvalue($items['KERN_MODULE'][0], ' ', '=', '"')

object.name = $kv_kern_mod["name"]

if $kv_syscall["tty"] != "(none)" then
    $tty = $kv_syscall["tty"]

    if (substr($tty, 0, 3) == "pts") and (substr($tty, 3, 1) != "/") then
        logon_service = "pts/" + substr($tty, 3, length($tty) - 3)
    else
        logon_service = $tty
    endif
endif

datafield1 = $comm  # Executable Name

chain_id = $eventid  # AuditD Event ID

reason = switch $kv_syscall["exit"]
    case "-1"   "Operation not permitted"
    case "-2"   "No such file or directory"
    case "-8"   "Exec format error"
    case "-9"   "Bad file descriptor"
    case "-11"  "Resource temporarily unavailable"
    case "-12"  "Cannot allocate memory"
    case "-14"  "Bad address"
    case "-16"  "Device or resource busy"
    case "-17"  "File exists"
    case "-19"  "No such device"
    case "-22"  "Invalid argument"
    case "-27"  "File too large"
    case "-74"  "Bad message"
    case "-126" "Required key not available"
endswitch

time = epoch_to_datetime(number($timestamp))

msgid = switch $kv_syscall["syscall"]
    case "313" "finit_module"
    case "350" "finit_module"
    case "175" "init_module"
    case "128" "init_module"
    case "176" "delete_module"
    case "129" "delete_module"
endswitch

importance = "medium"

category.generic = "Executable Module"
category.high = "Availability Management"
category.low = "Control"

event_src.title = "unix_like"
event_src.subsys = "auditd"

# Декодируем HEX, если требуется
$key_raw = strip($kv_syscall["key"], '"', '"')
if $key_raw == $kv_syscall["key"] then
    $key_raw = hex_to_string($kv_syscall["key"])
endif

event_src.rule = if $key_raw != "(null)" then $key_raw else null endif

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
        else
            event_src.ip = recv_ipv4
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
        else
            event_src.hostname = $node
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

event_src.category = "Operating system"

id = "PT_UNIX_like_auditd_syslog_structured_syscall_kernel_module"
