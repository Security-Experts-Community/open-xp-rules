JSON = ''
COND = (
    $eventid != null
    and $node != null
    and $timestamp != null
    and (
        (
            find_substr($items["SYSCALL"][0], "arch=c000003e") != null
            and (
                find_substr($items["SYSCALL"][0], "syscall=83") != null      # mkdir
                or find_substr($items["SYSCALL"][0], "syscall=258") != null  # mkdirat
                or find_substr($items["SYSCALL"][0], "syscall=84") != null   # rmdir
            )
        )
        or (
            find_substr($items["SYSCALL"][0], "arch=40000003") != null
            and (
                find_substr($items["SYSCALL"][0], "syscall=39") != null      # mkdir
                or find_substr($items["SYSCALL"][0], "syscall=296") != null  # mkdirat
                or find_substr($items["SYSCALL"][0], "syscall=40") != null   # rmdir
            )
        )
    )
)

$kv_syscall = keyvalue($items['SYSCALL'][0], ' ', '=')

if $kv_syscall["auid"] != "4294967295" then
    subject = "account"
endif

if in_list(["83", "39", "258", "296"], $kv_syscall["syscall"]) then
    action = "create"
else
    action = "remove"
endif

object = "file_object"

if $kv_syscall["success"] == "yes" then
    status = "success"
else
    status = "failure"
endif

# Декодируем HEX, если требуется
$auid_en = strip($kv_syscall["AUID"], '"', '"')
if $auid_en == $kv_syscall["AUID"] then
    $auid_en = hex_to_string($kv_syscall["AUID"])
endif

$auid_list = csv($auid_en, "@", "")

# Декодируем HEX, если требуется
$euid_en = strip($kv_syscall["EUID"], '"', '"')
if $euid_en == $kv_syscall["EUID"] then
    $euid_en = hex_to_string($kv_syscall["EUID"])
endif

$euid_list = csv($euid_en, "@", "")

if $kv_syscall["auid"] != "4294967295" then
    subject.account.name = $auid_list[0]
    subject.account.domain = $auid_list[1]

    subject.account.id = $kv_syscall["auid"]
else
    subject.account.name = $euid_list[0]
    subject.account.domain = $euid_list[1]

    subject.account.id = $kv_syscall["euid"]
endif

subject.account.privileges = $kv_syscall["euid"]
subject.account.session_id = $kv_syscall["ses"]

subject.process.id = $kv_syscall["pid"]
subject.process.parent.id = $kv_syscall["ppid"]

# SYSCALL::exe
if not in_list(["<no_memory>", "<too_long>", "(null)"], $kv_syscall["exe"]) then
    # Декодируем HEX, если требуется
    $exe = strip($kv_syscall["exe"], '"', '"')
    if $exe == $kv_syscall["exe"] then
        $exe = hex_to_string($kv_syscall["exe"])
    endif
endif

$process_path_and_name = csv($exe, "/", "")
$process_name = $process_path_and_name[length($process_path_and_name) - 1]
$process_path = strip($exe, "", $process_name)

subject.process.fullpath = $exe
subject.process.name = $process_name
subject.process.path = $process_path

# CWD
if $items["CWD"] != null then
    $kv_cwd = keyvalue($items['CWD'][0], ' ', '=', '"')
    
    if not in_list(["<no_memory>", "<too_long>", "(null)"], $kv_cwd["cwd"]) then
        # Декодируем HEX, если требуется
        $cwd_raw = strip($kv_cwd["cwd"], '"', '"')
        if $cwd_raw == $kv_cwd["cwd"] then
            $cwd_raw = hex_to_string($kv_cwd["cwd"])
        endif
        
        if substr($cwd_raw, -1, 1) != "/" then
            $cwd = $cwd_raw + "/"
        else
            $cwd = $cwd_raw
        endif
    endif
endif

subject.process.cwd = $cwd_raw

# PROCTITLE
if $items["PROCTITLE"] != null then
    $kv_proctitle = keyvalue($items["PROCTITLE"][0], ' ', '=')

    $process_proctitle = strip($kv_proctitle["proctitle"], '"', '"')
    if $process_proctitle == "(null)" then
        $process_proctitle = null
    elif $process_proctitle == $kv_proctitle["proctitle"] then
        $cmdline_raw = hex_to_string($kv_proctitle["proctitle"])

        # stripping corrupted char
        if $cmdline_raw == $kv_proctitle["proctitle"] then
            $cmdline_raw = hex_to_string(substr($kv_proctitle["proctitle"], 0, 254))
        endif
        if $cmdline_raw == $kv_proctitle["proctitle"] then
            $cmdline_raw = hex_to_string(substr($kv_proctitle["proctitle"], 0, 253))
        endif
        if $cmdline_raw == $kv_proctitle["proctitle"] then
            $cmdline_raw = hex_to_string(substr($kv_proctitle["proctitle"], 0, 252))
        endif

        $process_proctitle = replace($cmdline_raw, " ", "\u0000")
    endif
endif

subject.process.meta = $process_proctitle  # PROCTITLE

# Декодируем HEX, если требуется
$egid_en = strip($kv_syscall["EGID"], '"', '"')
if $egid_en == $kv_syscall["EGID"] then
    $egid_en = hex_to_string($kv_syscall["EGID"])
endif

object.account.group = coalesce($egid_en, $kv_syscall["egid"])

object.account.name = $euid_list[0]
object.account.domain = $euid_list[1]

object.account.id = $kv_syscall["euid"]

# PATH
$path_list = if $items["PATH"] != null then $items["PATH"] else [] endif
 
if (number($kv_syscall["items"]) != length($path_list)) and not in_list(["0", null], $kv_syscall["items"]) then
    drop
endif

$kv_path_0 = keyvalue($path_list[0], ' ', '=')
$kv_path_1 = keyvalue($path_list[1], ' ', '=')
$kv_path_2 = keyvalue($path_list[2], ' ', '=')
$kv_path_3 = keyvalue($path_list[3], ' ', '=')
$kv_path_4 = keyvalue($path_list[4], ' ', '=')

switch $kv_path_0["item"]
    case "0" $item_0 = $kv_path_0
    case "1" $item_1 = $kv_path_0
    case "2" $item_2 = $kv_path_0
    case "3" $item_3 = $kv_path_0
    case "4" $item_4 = $kv_path_0
endswitch
switch $kv_path_1["item"]
    case "0" $item_0 = $kv_path_1
    case "1" $item_1 = $kv_path_1
    case "2" $item_2 = $kv_path_1
    case "3" $item_3 = $kv_path_1
    case "4" $item_4 = $kv_path_1
endswitch
switch $kv_path_2["item"]
    case "0" $item_0 = $kv_path_2
    case "1" $item_1 = $kv_path_2
    case "2" $item_2 = $kv_path_2
    case "3" $item_3 = $kv_path_2
    case "4" $item_4 = $kv_path_2
endswitch
switch $kv_path_3["item"]
    case "0" $item_0 = $kv_path_3
    case "1" $item_1 = $kv_path_3
    case "2" $item_2 = $kv_path_3
    case "3" $item_3 = $kv_path_3
    case "4" $item_4 = $kv_path_3
endswitch
switch $kv_path_4["item"]
    case "0" $item_0 = $kv_path_4
    case "1" $item_1 = $kv_path_4
    case "2" $item_2 = $kv_path_4
    case "3" $item_3 = $kv_path_4
    case "4" $item_4 = $kv_path_4
endswitch

$item_0_type = coalesce($item_0["nametype"], $item_0["objtype"])
$item_1_type = coalesce($item_1["nametype"], $item_1["objtype"])
$item_2_type = coalesce($item_2["nametype"], $item_2["objtype"])
$item_3_type = coalesce($item_3["nametype"], $item_3["objtype"])
$item_4_type = coalesce($item_4["nametype"], $item_4["objtype"])

$items_len = number($kv_syscall["items"])
$items_list = [$item_0, $item_1, $item_2, $item_3, $item_4]
$items_types = [$item_0_type, $item_1_type, $item_2_type, $item_3_type, $item_4_type]

# Проблемы с директорией
if ($items_len == 1) and ($items_types[0] == "UNKNOWN") then
    $create_delete = $items_list[0]
endif

if $items_types[0] == "PARENT" then
    $parent = $items_list[0]
endif

$cnt = 1

# Добираемся с 1-й попытки
if ($items_types[$cnt] == "PARENT") and in_list(["CREATE", "DELETE"], $items_types[$cnt+1]) then
    $create_delete = $items_list[$cnt+1]   
    $cnt = $cnt + 2
elif in_list(["CREATE", "DELETE"], $items_types[$cnt]) then
    $create_delete = $items_list[$cnt]
    $cnt = $cnt + 1
endif

# Добираемся со 2-й попытки
if ($items_types[$cnt] == "PARENT") and in_list(["CREATE", "DELETE"], $items_types[$cnt+1]) then
    $create_delete = $items_list[$cnt+1]
elif in_list(["CREATE", "DELETE"], $items_types[$cnt]) then
    $create_delete = $items_list[$cnt]
endif

# PATH::PARENT
if not in_list(["<no_memory>", "<too_long>", "(null)", null], $parent["name"]) then
    # Декодируем HEX, если требуется
    $parent_name = strip($parent["name"], '"', '"')
    if $parent_name == $parent["name"] then
        $parent_name = hex_to_string($parent["name"])
    endif

    # Обрабатываем относительные пути
    if (substr($parent_name, 0, 2) == "./") or (substr($parent_name, 0, 1) != "/") then
        $parent_name = $cwd + strip($parent_name, "./", "")
    endif

    # Добавляем в конец '/', если его нет
    if substr($parent_name, -1, 1) != "/" then
        $parent_name = $parent_name + "/"
    endif
    
    $file_path = $parent_name
endif

# PATH::CREATE/DELETE
if not in_list(["<no_memory>", "<too_long>", "(null)", null], $create_delete["name"]) then
    # Декодируем HEX, если требуется
    $create_delete_name = strip($create_delete["name"], '"', '"')
    if $create_delete_name == $create_delete["name"] then
        $create_delete_name = hex_to_string($create_delete["name"])
    endif
    
    # Обрабатываем относительные пути
    if (substr($create_delete_name, 0, 2) == "./") or (substr($create_delete_name, 0, 1) != "/") then
        if $kv_cwd["cwd"] != "/" then
            $create_delete_name = $kv_cwd["cwd"] + "/" + strip($create_delete_name, "./", "")
        else
            $create_delete_name = $kv_cwd["cwd"] + strip($create_delete_name, "./", "")
        endif
    endif
    
    $file_list = csv($create_delete_name, "/", "")
    $file_fullpath = $create_delete_name
    $file_name = $file_list[length($file_list)-1]
    $file_path = strip($file_fullpath, "", $file_name)
endif

object.type = "directory"

object.fullpath = $file_fullpath
object.path = $file_path
object.name = $file_name
object.id = $create_delete["inode"]

if $kv_syscall["tty"] != "(none)" then
    $tty = $kv_syscall["tty"]

    if (substr($tty, 0, 3) == "pts") and (substr($tty, 3, 1) != "/") then
        logon_service = "pts/" + substr($tty, 3, length($tty) - 3)
    else
        logon_service = $tty
    endif
endif

# Декодируем HEX, если требуется
$comm = strip($kv_syscall["comm"], '"', '"')
if $comm == $kv_syscall["comm"] then
    $comm = hex_to_string($kv_syscall["comm"])
endif

datafield1 = $comm  # Executable Name

# Декодируем HEX, если требуется
$ouid_en = strip($create_delete["OUID"], '"', '"')
if $ouid_en == $create_delete["OUID"] then
    $ouid_en = hex_to_string($create_delete["OUID"])
endif

datafield2 = $ouid_en  # Owner User Name
datafield3 = substr($create_delete["mode"], -4, 4)  # File Access Mode

numfield2 = $create_delete["ouid"]  # Owner User ID

chain_id = $eventid  # AuditD Event ID

reason = switch $kv_syscall["exit"]
    case "-1"   "Operation not permitted"
    case "-2"   "No such file or directory"
    case "-9"   "Bad file descriptor"
    case "-12"  "Cannot allocate memory"
    case "-13"  "Permission denied"
    case "-14"  "Bad address"
    case "-16"  "Device or resource busy"
    case "-17"  "File exists"
    case "-20"  "Not a directory"
    case "-22"  "Invalid argument"
    case "-28"  "No space left on device"
    case "-30"  "Read-only file system"
    case "-31"  "Too many links"
    case "-36"  "File name too long"
    case "-39"  "Directory not empty"
    case "-40"  "Too many levels of symbolic links"
    case "-122" "Disk quota exceeded"
endswitch

time = epoch_to_datetime(number($timestamp))

msgid = switch $kv_syscall["syscall"]
    case "83"  "mkdir"
    case "39"  "mkdir"
    case "258" "mkdirat"
    case "296" "mkdirat"
    case "84"  "rmdir"
    case "40"  "rmdir"
endswitch

importance = "low"

category.generic = "File System Object"
category.high = "System Management"
category.low = "Manipulation"

event_src.title = "unix_like"
event_src.subsys = "auditd"

# Декодируем HEX, если требуется
$key_raw = strip($kv_syscall["key"], '"', '"')
if $key_raw == $kv_syscall["key"] then
    $key_raw = hex_to_string($kv_syscall["key"])
endif

event_src.rule = if $key_raw != "(null)" then $key_raw else null endif

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
        else
            event_src.ip = recv_ipv4
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
        else
            event_src.hostname = $node
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

event_src.category = "Operating system"

id = "PT_UNIX_like_auditd_syslog_structured_syscall_folder_modify"