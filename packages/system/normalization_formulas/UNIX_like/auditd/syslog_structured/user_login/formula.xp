JSON = ''
COND = (
    $eventid != null
    and $node != null
    and $timestamp != null
    and $items["USER_LOGIN"] != null
)

$kv_main = keyvalue($items["USER_LOGIN"][0], " ", "=", "'")
$kv_msg = keyvalue($kv_main['msg'], ' ', '=', '"')

subject = "account"
action = "login"
object = "system"

if ($kv_msg["res"] == "success") then
    status = "success"
else
    status = "failure"
endif

subformula "parse_enriched"
    TEXT = '{STRING+} res={WORD}\' UID={$uid_1=LITERAL*} AUID={$auid=LITERAL*} UID={$uid_2=LITERAL*}'
endsubformula

subformula "parse_enriched"
    TEXT = '{REST}'
endsubformula

submessage("TEXT", "parse_enriched", $items["USER_LOGIN"][0])

# Декодируем HEX, если требуется
$id_en = strip($kv_main["ID"], '"', '"')
if $id_en == $kv_main["ID"] then
    $id_en = hex_to_string($kv_main["ID"])
endif

$id_list = csv($id_en, "@", "")

# Декодируем HEX, если требуется
$acct = strip($kv_msg["acct"], '"', '"')
if $acct == $kv_msg["acct"] then
    $acct = hex_to_string($kv_msg["acct"])
endif

$acct_list = csv($acct, "@", "")

# Декодируем HEX, если требуется
$uid_1_en = strip($uid_1, '"', '"')
if $uid_1_en == $uid_1 then
    $uid_1_en = hex_to_string($uid_1)
endif

$uid_1_list = csv($uid_1_en, "@", "")

# Декодируем HEX, если требуется
$uid_2_en = strip($uid_2, '"', '"')
if $uid_2_en == $uid_2 then
    $uid_2_en = hex_to_string($uid_2)
endif

$uid_2_list = csv($uid_2_en, "@", "")

if ($kv_msg["id"] != null) then  # OpenSSH & Login Terminal
    subject.account.id = $kv_msg["id"]
    subject.account.name = $id_list[0]
    subject.account.domain = $id_list[1]
    
elif ($kv_msg["uid"] != null) then  # GNOME Display Manager
    subject.account.id = $kv_msg["uid"]
    subject.account.name = $uid_2_list[0]
    subject.account.domain = $uid_2_list[1]
    
elif ($kv_msg["acct"] != null) then  # Light Display Manager
    subject.account.name = $acct_list[0]
    subject.account.domain = $acct_list[1]  
endif

if ($kv_main["auid"] != "4294967295") then
    subject.account.session_id = $kv_main["ses"]
endif

if ($kv_msg["res"] == "success") then
    subject.state = "logged-in"
endif

if $kv_msg["exe"] != "?" then
    # Декодируем HEX, если требуется
    $exe = strip($kv_msg["exe"], '"', '"')
    if $exe == $kv_msg["exe"] then
        $exe = hex_to_string($kv_msg["exe"])
    endif
    
    $process_path_and_name = csv($exe, "/", "")
    $process_fullpath = $exe
    $process_name = $process_path_and_name[length($process_path_and_name) - 1]
    $process_path = strip($exe, "", $process_name)
endif

object.process.fullpath = $process_fullpath
object.process.name = $process_name
object.process.path = $process_path
object.process.id = $kv_main["pid"]

if $kv_msg["terminal"] != "?" then
    logon_service = strip($kv_msg["terminal"], "/dev/", "")
endif

if action == "login" then
    if $process_name == "sshd" then
        logon_auth_method = "remote"
        
        if not in_list([$kv_msg["addr"], "?"], $kv_msg["hostname"]) then
            src.hostname = $kv_msg["hostname"]
        endif

        if ($kv_msg["addr"] != "?") then
            src.ip = $kv_msg["addr"]
        endif
    else
        logon_auth_method = "local"
    endif
endif

chain_id = $eventid  # AuditD Event ID

category.generic = "Operating System"
category.high = "Access Management"
category.low = "Communication"

time = epoch_to_datetime(number($timestamp))
msgid = "USER_LOGIN"
importance = "low"

event_src.title = "unix_like"
event_src.subsys = "auditd"
event_src.category = "Operating system"

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
            $dst_ip = $node
        else
            event_src.ip = recv_ipv4
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
            $dst_fqdn = event_src.fqdn
            $dst_hostname = event_src.hostname
        else
            event_src.hostname = $node
            $dst_hostname = event_src.hostname
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

if logon_auth_method == "remote" then
    dst.ip = $dst_ip
    dst.fqdn = $dst_fqdn
    dst.hostname = $dst_hostname
endif

id = "PT_UNIX_like_auditd_syslog_structured_user_login"
