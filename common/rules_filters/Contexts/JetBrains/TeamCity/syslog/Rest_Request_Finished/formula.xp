#<190>Sep 27 20:12:08 teamcity teamcity-rest.log: [2023-09-27 20:10:52,620]  DEBUG [p-nio-80-exec-7] - er.rest.APIController/rest-api - REST API  request processing finished in 3ms, status code: 200, request: GET '/app/rest/users', from client 10.125.3.2:57267, no user-agent, authenticated as user with id=-42

TEXT = "{'<'NUMBER'>'?}{':'?}{DATETIME} {event_src.ip=IPV4|event_src.ip=IPV6|event_src.hostname=HOSTNAME|} teamcity-{WORD}.log: [{$time=UNTIL(']')} {WORD} [{UNTIL(']')} - er.rest.APIController/rest-api - REST API  request processing finished in {datafield1=NUMBER}ms, status code: {$code=NUMBER},request: {object.type=WORD} {$object.query=UNTIL(',')} from client {src.ip=IPV4|src.ip=IPV6|'<unknown IP address>'}:{src.port=NUMBER}, {object.process.meta=REST}"

action = "execute"
object = "request"

if $code == 200 then
    status = "success"
else
    status = "failure"
endif
 
if find_substr(object.process.meta, "authenticated as") != null then
    submessage("TEXT", "get_object_id", object.process.meta)
endif

subformula "get_chain_id"
    TEXT = "{$clear_time=DATETIME},{$ms=NUMBER}"
    
    numfield3 = datetime_to_epoch_ms($clear_time) + $ms  # Millisecond Timestamp (to understand the events order)
endsubformula

object.query = strip($object.query, "'", "'")

subformula "get_object_id"
    TEXT = "{UNTIL('authenticated as user with id=')}{object.account.id=NUMBER}"
    
    if number(object.account.id) == -42 then
        object.account.privileges = "super_user"
    elif number(object.account.id) == -1000 then
        object.account.privileges = "guest_user"
    endif
endsubformula

submessage("TEXT", "get_chain_id", $time)

#datafield1 - duration in ms

time = replace($time, ".", ",")
importance = "medium"

event_src.vendor = "jetbrains"
event_src.title = "teamcity"
event_src.category = "Other"

id = "PT_JetBrains_TeamCity_syslog_Rest_Request_Finished"