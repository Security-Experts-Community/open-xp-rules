EVENTLOG = 'EventID="4689"'
COND = lower($Channel)=="security" and $Provider["Name"]=="Microsoft-Windows-Security-Auditing"

subject = "account"
action = "stop"
object = "process"
status = "success"

subject.account.name = if $Data["SubjectUserName"] != "-" then lower($Data["SubjectUserName"]) else null endif
subject.account.domain = if $Data["SubjectDomainName"] != "-" then lower($Data["SubjectDomainName"]) else null endif
subject.account.id = if $Data["SubjectUserSid"] != "S-1-0-0" then $Data["SubjectUserSid"] else null endif
if subject.account.id == null and subject.account.name != null then
    subject.account.id = "synthetic:" + subject.account.name
    if subject.account.domain != null then
        subject.account.id = subject.account.id + "@" + subject.account.domain
    endif
endif
subject.account.session_id = maybe_number16($Data["SubjectLogonId"])
subject.account.privileges = switch $Data["TokenElevationType"]
    case "%%1936" "TokenElevationTypeDefault"
    case "%%1937" "TokenElevationTypeFull"
    case "%%1938" "TokenElevationTypeLimited"
endswitch

object.account.session_id = subject.account.session_id
object.account.name = subject.account.name
object.account.domain = subject.account.domain
object.account.id = subject.account.id

object.process.id = maybe_number16($Data["ProcessId"])
object.process.fullpath = lower($Data["ProcessName"])
$process_path = csv(object.process.fullpath, "\\", "")
object.process.name = $process_path[length($process_path) - 1]
object.process.path = strip(object.process.fullpath, "", object.process.name)

reason = $Data["Status"]

time = $TimeCreated["SystemTime"]
msgid = $EventID
importance = "info"

category.generic = "Process"
category.high = "Availability Management"
category.low = "Control"

event_src.vendor = "microsoft"
event_src.title = "windows"
event_src.subsys = $Channel
if find_substr($Computer, '.') != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, find_substr($Computer, '.')))
else
    event_src.hostname = lower($Computer)
endif
event_src.category = "Operating system"

id = "PT_Microsoft_Windows_eventlog_4689_A_process_has_exited"

# -- DEPRECATED --
# lines from here will be deleted in future releases
subject.id = object.account.id
subject.name = object.account.name
subject.domain = object.account.domain
object.id = object.process.id
object.name = object.process.name
object.path = object.process.path
object.state = reason
datafield1 = object.account.session_id
