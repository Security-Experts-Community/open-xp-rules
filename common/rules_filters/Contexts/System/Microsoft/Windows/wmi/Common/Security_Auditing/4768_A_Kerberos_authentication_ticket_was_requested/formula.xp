!JSON = "EventCode=4768"
!COND = $Logfile=="Security" and $SourceName=="Microsoft-Windows-Security-Auditing"

subject = "account"
action = "login"
object = "system"

if $EventType == "4" then
    status = "success"
elif $EventType == "5" then
    status = "failure"
endif

if find_substr(lower($InsertionStrings[0]), "x509n") != null then
    $kv = keyvalue(strip(lower($InsertionStrings[0]), "x509n:<s>", ""), ",", "=")
    $username = lower($kv["e"])
else
    $username = lower($InsertionStrings[0])
endif

subject.account.id = $InsertionStrings[2]
$t_at = find_substr($username, "@")
if $t_at != null then
    subject.account.name = lower(substr($username , 0, $t_at))
    subject.account.domain = lower(substr($username , $t_at+1, length($username)))
else
    subject.account.name = lower($username)
    subject.account.domain = lower($InsertionStrings[1])
endif

if (maybe_ipv4(strip($InsertionStrings[9], "::ffff:","")) != null) || (maybe_ipv6($InsertionStrings[9]) != null) then
    $ip = strip($InsertionStrings[9], "::ffff:","")
    if in_subnet($ip, "127.0.0.0/8") or $ip == "::1" then
        src.hostname = "localhost"
    else
        src.ip = $ip
    endif
endif

if $InsertionStrings[10] != "-" then
    src.port = $InsertionStrings[10]
endif

logon_type = $InsertionStrings[8]               # PreAuthType
logon_service = $InsertionStrings[3]

if subject.account.domain != lower($InsertionStrings[1]) and subject.account.id == null then
    datafield1 = lower($InsertionStrings[1])
endif

datafield2 = switch string(logon_type)          
    case "0" "Logon without Pre-Authentication"
    case "2" "Standard password authentication"
    case "11" "The ETYPE-INFO pre-authentication"
    case "15" "Smart Card logon authentication"
    case "16" "Request sent to KDC in Smart Card authentication scenarios"
    case "17" "Smart Card logon authentication"
    case "19" "The ETYPE-INFO2 pre-authentication"
    case "20" "KDC Referrals tickets"
    case "138" "Logon using Kerberos Armoring (FAST)"
endswitch

datafield4 = switch lower($InsertionStrings[6])                      
    case "0x0" "No error"
    case "0x1" "Client's entry in database has expired"
    case "0x2" "Server's entry in database has expired"
    case "0x3" "Requested protocol version number not supported"
    case "0x4" "Client's key encrypted in old master key"
    case "0x5" "Server's key encrypted in old master key"
    case "0x6" "Client not found in Kerberos database"
    case "0x7" "Server not found in Kerberos database"
    case "0x8" "Multiple principal entries in database"
    case "0x9" "The client or server has a null key"
    case "0xa" "Ticket not eligible for postdating"
    case "0xb" "Requested starttime is later than end time"
    case "0xc" "KDC policy rejects request"
    case "0xd" "KDC cannot accommodate requested option"
    case "0xe" "KDC has no support for encryption type"
    case "0xf" "KDC has no support for checksum type"
    case "0x10" "KDC has no support for padata type"
    case "0x11" "KDC has no support for transited type"
    case "0x12" "Clients credentials have been revoked"
    case "0x13" "Credentials for server have been revoked"
    case "0x14" "TGT has been revoked"
    case "0x15" "Client not yet valid"
    case "0x16" "Server not yet valid"
    case "0x17" "Password has expired"
    case "0x18" "Pre-authentication information was invalid"
    case "0x19" "Additional pre- authentication required"
    case "0x1a" "Requested server and ticket don't match"
    case "0x1b" "Server principal valid for user2user only"
    case "0x1c" "KDC Policy rejects transited path"
    case "0x1d" "A service is not available"
    case "0x1f" "Integrity check on decrypted field failed"
    case "0x20" "Ticket expired"
    case "0x21" "Ticket not yet valid"
    case "0x22" "Request is a replay"
    case "0x23" "The ticket isn't for us"
    case "0x24" "Ticket and authenticator don't match"
    case "0x25" "Clock skew too great"
    case "0x26" "Incorrect net address"
    case "0x27" "Protocol version mismatch"
    case "0x28" "Invalid msg type"
    case "0x29" "Message stream modified"
    case "0x2a" "Message out of order"
    case "0x2c" "Specified version of key is not available"
    case "0x2d" "Service key not available"
    case "0x2e" "Mutual authentication failed"
    case "0x2f" "Incorrect message direction"
    case "0x30" "Alternative authentication method required"
    case "0x31" "Incorrect sequence number in message"
    case "0x32" "Inappropriate type of checksum in message"
    case "0x33" "Policy rejects transited path"
    case "0x34" "Response too big for UDP"
    case "0x3c" "Generic error (description in e-text)"
    case "0x3d" "Field is too long for this implementation"
    case "0x3e" "The client trust failed or is not implemented"
    case "0x3f" "The KDC server trust failed or could not be verified"
    case "0x40" "The signature is invalid"
    case "0x41" "A higher encryption level is needed"
    case "0x42" "User-to-user authorization is required"
    case "0x43" "No TGT available to validate USER-TO-USER"
    case "0x44" "Incorrect domain or principal"
    case "0x45" "Ticket must be for USER-TO-USER"
    case "0x80000001" "More data is available"
    case "0x80000002" "The Kerberos service is not running"
endswitch

datafield5 = $InsertionStrings[7]               

datafield6 = switch lower(datafield5)           
     case "0x1" "DES-CBC-CRC"
     case "0x3" "DES-CBC-MD5"
     case "0x11" "AES128-CTS-HMAC-SHA1-96"
     case "0x12" "AES256-CTS-HMAC-SHA1-96"
     case "0x17" "RC4-HMAC"
     case "0x18" "RC4-HMAC-EXP"
     case "0xffffffff" "-"
endswitch

datafield8 = $InsertionStrings[5]               
datafield9 = $InsertionStrings[4]               # Logon service sid
if $InsertionStrings[11] != "" then
    datafield7 = $InsertionStrings[11]          # Certificate Issuer Name
endif

reason = lower($InsertionStrings[6])
time = $TimeGenerated
msgid = $EventCode

importance = "info"

category.generic = "Operating System"
category.high = "Access Management"
category.low = "Communication"

if find_substr($ComputerName, '.') != null then
    event_src.fqdn = lower($ComputerName)
    event_src.hostname = lower(substr($ComputerName, 0, find_substr($ComputerName, '.')))
else
    event_src.hostname = lower($ComputerName)
endif
event_src.subsys = $Logfile
event_src.category = "AAA"
event_src.title = "windows"
event_src.vendor = "microsoft"

id = "PT_Microsoft_Windows_wmi_4768_A_Kerberos_authentication_ticket_was_requested"

