EVENTLOG = 'EventID="5140"'
COND = lower($Channel)=="security" and $Provider["Name"]=="Microsoft-Windows-Security-Auditing"

subject = "account"
action = "access"
object = "file_object"
if $Keywords == "0x8020000000000000" then
    status = "success"
elif $Keywords == "0x8010000000000000" then
    status = "failure"
endif

subject.account.name = if $Data["SubjectUserName"] != "-" then lower($Data["SubjectUserName"]) else null endif
subject.account.domain = if $Data["SubjectDomainName"] != "-" then lower($Data["SubjectDomainName"]) else null endif
subject.account.id = if $Data["SubjectUserSid"] != "S-1-0-0" then $Data["SubjectUserSid"] else null endif
if subject.account.id == null and subject.account.name != null then
    subject.account.id = "synthetic:" + subject.account.name
    if subject.account.domain != null then
        subject.account.id = subject.account.id + "@" + subject.account.domain
    endif
endif
subject.account.session_id = maybe_number16($Data["SubjectLogonId"])
subject.account.privileges = $Data["AccessList"]

object.type = $Data["ObjectType"]

object.storage.id = $Data["ShareName"]

object.storage.fullpath = lower(strip($Data["ShareLocalPath"], "\\??\\", "\\"))
$name_and_path_local = csv(object.storage.fullpath, "\\", "'")
if $name_and_path_local[0] == object.storage.fullpath then
    object.storage.path = object.storage.fullpath 
else
    object.storage.name = $name_and_path_local[length($name_and_path_local) - 1]    
    object.storage.path = strip(object.storage.fullpath, "", object.storage.name)     
endif

object.fullpath = lower(strip($Data["ShareName"], "\\\\*", "\\"))
$name_and_path_public = csv(object.fullpath, "\\", "'")
if $name_and_path_public[0] == object.fullpath then
    object.path = object.fullpath 
else
    object.name = $name_and_path_public[length($name_and_path_public) - 1]    
    object.path = strip(object.fullpath, "", object.name)     
endif

datafield3 = $Data["AccessMask"]

src.ip = $Data["IpAddress"]
if $Data["IpPort"] != "-" then
    src.port = $Data["IpPort"]
endif
if find_substr($Computer, '.') != null then
    dst.fqdn = lower($Computer)
    dst.hostname = lower(substr($Computer, 0, find_substr($Computer, '.')))
else
    dst.hostname = lower($Computer)
endif

time = $TimeCreated["SystemTime"]
msgid = $EventID
importance = "medium"

category.generic = "File System Object"
category.high = "System Management"
category.low = "Manipulation"

event_src.vendor = "microsoft"
event_src.title = "windows"
event_src.subsys = $Channel
event_src.category = "Operating system"
event_src.hostname = dst.hostname
event_src.fqdn = dst.fqdn

id = "PT_Microsoft_Windows_eventlog_5140_A_network_share_object_was_accessed"

