EVENTLOG = 'EventID="5156"'
COND = lower($Channel)=="security" and $Provider["Name"]=="Microsoft-Windows-Security-Auditing"

subject = "rule"
action = "allow"
object = "connection"
status = "success"

subject.id = $Data["FilterRTID"]

object.process.id = $Data["ProcessID"]
$process_fullpath = lower($Data["Application"])
$name_and_path = csv($process_fullpath, "\\", "'")
if $name_and_path[0] == $process_fullpath then
    object.process.name = $process_fullpath
else
    object.process.name = $name_and_path[length($name_and_path) - 1]
    object.process.path = strip($process_fullpath, "", object.process.name)
    object.process.fullpath = $process_fullpath
endif

object.property = "ALE layer"
$object_value_raw = switch $Data["LayerName"]
    case "%%14610" "Receive/Accept"
    case "%%14611" "Connect"
endswitch
object.value = coalesce($object_value_raw, $Data["LayerName"])

direction = switch $Data["Direction"]
    case "%%14592" "ingress"
    case "%%14593" "egress"
endswitch

src.ip = $Data["SourceAddress"]
src.port = $Data["SourcePort"]
dst.ip = $Data["DestAddress"]
dst.port = $Data["DestPort"]

protocol = $Data["Protocol"]

datafield5 = $Data["LayerRTID"]     # layer id

time = $TimeCreated["SystemTime"]
msgid = $EventID
importance = "info"

category.generic = "Connection"
category.high = "Network Interaction Management"
category.low = "Control"

event_src.vendor = "microsoft"
event_src.title = "windows"
event_src.subsys = $Channel
if find_substr($Computer, '.') != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, find_substr($Computer, '.')))
else
    event_src.hostname = lower($Computer)
endif
event_src.category = "Operating system"

id = "PT_Microsoft_Windows_eventlog_5156_WFP_has_permitted_connection"

