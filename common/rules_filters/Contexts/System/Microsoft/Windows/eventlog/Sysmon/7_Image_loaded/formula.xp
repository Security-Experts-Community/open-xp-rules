EVENTLOG = 'EventID="7"'

COND = $Provider["Name"] == "Microsoft-Windows-Sysmon"

subject = "process"
action = "start"
object = "module"
status = "success"

subject.process.id = $Data["ProcessId"]
$process_fullpath = lower($Data["Image"])
$process_fullpath_csv = csv($process_fullpath, "\\", "'")
$process_name = $process_fullpath_csv[length($process_fullpath_csv) - 1]
subject.process.name = $process_name
if strip($process_fullpath, "", $process_name) != '' then
    subject.process.path = strip($process_fullpath, "", $process_name)
endif
subject.process.fullpath = $process_fullpath
subject.process.guid = strip($Data['ProcessGuid'], "{", "}")

$subject_name_domain = csv($Data["User"], "\\", "'")
if length($subject_name_domain) > 1 then
    subject.account.domain = lower($subject_name_domain[0])
    subject.account.name = lower($subject_name_domain[1])
    subject.account.id = "synthetic:" + subject.account.name
    if subject.account.domain != null then
        subject.account.id = subject.account.id + "@" + subject.account.domain
    endif
elif $Data["User"] != '-' then
    subject.account.id = $Data["User"]
endif

$object_process_fullpath = lower($Data["ImageLoaded"])
$object_process_fullpath_csv = csv($object_process_fullpath, "\\", "'")
$object_process_name = $object_process_fullpath_csv[length($object_process_fullpath_csv) - 1]
object.process.name = $object_process_name
if strip($object_process_fullpath, "", $object_process_name) != '' then
    object.process.path = strip($object_process_fullpath, "", $object_process_name)
endif
object.process.fullpath = $object_process_fullpath
object.process.original_name = $Data["OriginalFileName"]

if $Data["Description"] != null then
    object.process.meta = "Description:" + $Data["Description"] + " | Product:" + $Data["Product"] + " | Company:" + $Data["Company"]
endif

$temp_object_hash = keyvalue($Data["Hashes"], ",", "=")
if $temp_object_hash["SHA256"] != null then
    object.process.hash.sha256 = $temp_object_hash["SHA256"]
endif
if $temp_object_hash["MD5"] != null then
    object.process.hash.md5 = $temp_object_hash["MD5"]
endif
if $temp_object_hash["SHA1"] != null then
    object.process.hash.sha1 = $temp_object_hash["SHA1"]
endif
if $temp_object_hash["IMPHASH"] != null then
    object.process.hash.imphash = $temp_object_hash["IMPHASH"]
endif

object.property = "signature status"
if $Data["Signed"] == "true" then
    object.value = $Data["SignatureStatus"]  + " | " + $Data["Signature"]
elif lower($Data["Signed"]) == "failed: signing queue is full" then
    object.value = "undefined"
else
    object.value = "not signed"
endif

object.version = $Data["FileVersion"]

time = $Data["UtcTime"]
msgid = $EventID
importance = "low"

category.generic = "Executable Module"
category.high = "Availability Management"
category.low = "Control"

event_src.vendor = "microsoft"
event_src.title = "sysmon"
event_src.subsys = $Channel
event_src.rule = $Data["RuleName"]
if find_substr($Computer, '.') != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, find_substr($Computer, '.')))
else
    event_src.hostname = lower($Computer)
endif
event_src.category = "Other"

id = "PT_Microsoft_Windows_eventlog_Sysmon_7_Image_loaded"

