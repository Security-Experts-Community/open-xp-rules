EVENTLOG = 'EventID="10"'

COND = $Provider["Name"] == "Microsoft-Windows-Sysmon"

subject = "process"
action = "access"
object = "process"
status = "success"

subject.process.id = $Data["SourceProcessId"]
$process_fullpath = lower($Data["SourceImage"])
$process_fullpath_csv = csv($process_fullpath, "\\", "")
$process_name = $process_fullpath_csv[length($process_fullpath_csv) - 1]
subject.process.name = $process_name
if strip($process_fullpath, "", $process_name) != '' then
    subject.process.path = strip($process_fullpath, "", $process_name)
endif
subject.process.fullpath = $process_fullpath
subject.process.guid = strip($Data["SourceProcessGUID"], "{", "}")

$subject_name_domain = csv($Data["SourceUser"], "\\", "'")
if length($subject_name_domain) > 1 then
    subject.account.domain = lower($subject_name_domain[0])
    subject.account.name = lower($subject_name_domain[1])
    subject.account.id = "synthetic:" + subject.account.name
    if subject.account.domain != null then
        subject.account.id = subject.account.id + "@" + subject.account.domain
    endif
elif $Data["SourceUser"] != '-' then
    subject.account.id = $Data["SourceUser"]
endif

object.process.id = $Data["TargetProcessId"]
$object_process_fullpath = lower($Data["TargetImage"])
$object_process_path_csv = csv($object_process_fullpath, "\\", "")
$object_process_name = $object_process_path_csv[length($object_process_path_csv) - 1]
object.process.name = $object_process_name
if strip($object_process_fullpath, "", $object_process_name) != '' then
    object.process.path = strip($object_process_fullpath, "", $object_process_name)
endif
object.process.fullpath = $object_process_fullpath
object.process.guid = strip($Data["TargetProcessGUID"], "{", "}")

$object_name_domain = csv($Data["TargetUser"], "\\", "'")
if length($object_name_domain) > 1 then
    object.account.domain = lower($object_name_domain[0])
    object.account.name = lower($object_name_domain[1])
    object.account.id = "synthetic:" + object.account.name
    if object.account.domain != null then
        object.account.id = object.account.id + "@" + object.account.domain
    endif
elif $Data["TargetUser"] != '-' then
    object.account.id = $Data["TargetUser"]
endif

object.property = "GrantedAccess"
object.value = lower($Data["GrantedAccess"])

datafield5 = $Data["SourceThreadId"]  
datafield9 = $Data["CallTrace"]       

time = $Data["UtcTime"]
msgid = $EventID
importance = "low"

category.generic = "Process"
category.high = "System Management"
category.low = "Manipulation"

event_src.vendor = "microsoft"
event_src.title = "sysmon"
event_src.subsys = $Channel
event_src.rule = $Data["RuleName"]
if find_substr($Computer, '.') != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, find_substr($Computer, '.')))
else
    event_src.hostname = lower($Computer)
endif
event_src.category = "Other"

id = "PT_Microsoft_Windows_eventlog_Sysmon_10_Process_access"

