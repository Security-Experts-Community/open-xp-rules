EVENTLOG = 'EventID="11"'

COND = $Provider["Name"] == "Microsoft-Windows-Sysmon"

subject = "process"
action = "create"
object = "file_object"
status = "success"

subject.process.id = $Data["ProcessId"]
$process_fullpath = lower($Data["Image"])
$process_fullpath_csv = csv($process_fullpath, "\\", "'")
$process_name = $process_fullpath_csv[length($process_fullpath_csv) - 1]
subject.process.name = $process_name
if strip($process_fullpath, "", $process_name) != '' then
    subject.process.path = strip($process_fullpath, "", $process_name)
endif
subject.process.fullpath = $process_fullpath
subject.process.guid = strip($Data['ProcessGuid'], '{', '}')

$subject_name_domain = csv($Data["User"], "\\", "'")
if length($subject_name_domain) > 1 then
    subject.account.domain = lower($subject_name_domain[0])
    subject.account.name = lower($subject_name_domain[1])
    subject.account.id = "synthetic:" + subject.account.name
    if subject.account.domain != null then
        subject.account.id = subject.account.id + "@" + subject.account.domain
    endif
elif $Data["User"] != '-' then
    subject.account.id = $Data["User"]
endif

$object_fullpath = lower($Data["TargetFilename"])
$object_fullpath_csv = csv($object_fullpath,'\\','"')
$object_name = $object_fullpath_csv[length($object_fullpath_csv) - 1]
object.name = $object_name
if strip($object_fullpath, "", $object_name) != '' then
    object.path = strip($object_fullpath, "", $object_name)
endif
object.fullpath = $object_fullpath
object.property = "creation time"
object.value = datetime($Data["CreationUtcTime"])

time = $Data["UtcTime"]
msgid = $EventID
importance = "info"

category.generic = "File System Object"
category.high = "System Management"
category.low = "Manipulation"

event_src.vendor = "microsoft"
event_src.title = "sysmon"
event_src.subsys = $Channel
event_src.rule = $Data["RuleName"]
if find_substr($Computer, '.') != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, find_substr($Computer, '.')))
else
    event_src.hostname = lower($Computer)
endif
event_src.category = "Other"

id = "PT_Microsoft_Windows_eventlog_Sysmon_11_File_create"

