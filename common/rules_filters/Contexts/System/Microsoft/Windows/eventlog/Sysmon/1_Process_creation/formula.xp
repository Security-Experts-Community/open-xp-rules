EVENTLOG = 'EventID="1"'

COND = $Provider["Name"] == "Microsoft-Windows-Sysmon"

subject = "account"
action = "start"
object = "process"
status = "success"

$object_name_domain = csv($Data["User"], "\\", "'")
if length($object_name_domain) > 1 then
    object.account.domain = lower($object_name_domain[0])
    object.account.name = lower($object_name_domain[1])
    object.account.id = "synthetic:" + object.account.name
    if object.account.domain != null then
        object.account.id = object.account.id + "@" + object.account.domain
    endif
elif $Data["User"] != '-' then
    object.account.id = $Data["User"]
endif

object.account.session_id = maybe_number16($Data["LogonId"])
object.account.privileges = $Data["IntegrityLevel"]

if $Data["ParentUser"] == null then                               # old logic for old Sysmon
    subject.account.privileges = object.account.privileges
    subject.account.name = object.account.name
    subject.account.domain = object.account.domain
    subject.account.id = object.account.id
    subject.account.session_id = object.account.session_id
else
    $subject_name_domain = csv($Data["ParentUser"], "\\", "'")
    if length($subject_name_domain) > 1 then
        subject.account.domain = lower($subject_name_domain[0])
        subject.account.name = lower($subject_name_domain[1])
        subject.account.id = "synthetic:" + subject.account.name
        if subject.account.domain != null then
            subject.account.id = subject.account.id + "@" + subject.account.domain
        endif
    elif $Data["ParentUser"] != '-' then
        subject.account.id = $Data["ParentUser"]
    endif
    
    if $Data["ParentUser"] == $Data["User"] then                   
        subject.account.session_id = object.account.session_id
        subject.account.privileges = object.account.privileges
    endif
endif

object.process.id = $Data["ProcessId"]
$process_fullpath = lower($Data["Image"])
$process_fullpath_csv = csv($process_fullpath, "\\", "")
$process_name = $process_fullpath_csv[length($process_fullpath_csv) - 1]
object.process.name = $process_name
if strip($process_fullpath, "", $process_name) != '' then
    object.process.path = strip($process_fullpath, "", $process_name)
endif
object.process.fullpath = $process_fullpath
object.process.version = $Data["FileVersion"]
object.process.cmdline = $Data['CommandLine']
object.process.guid = strip($Data['ProcessGuid'], '{', '}')
object.process.original_name = $Data["OriginalFileName"]

$temp_object_hash = keyvalue($Data["Hashes"], ",", "=")
if $temp_object_hash["SHA256"] != null then
    object.process.hash.sha256 = $temp_object_hash["SHA256"]
endif
if $temp_object_hash["MD5"] != null then
    object.process.hash.md5 = $temp_object_hash["MD5"]
endif
if $temp_object_hash["SHA1"] != null then
    object.process.hash.sha1 = $temp_object_hash["SHA1"]
endif
if $temp_object_hash["IMPHASH"] != null then
    object.process.hash.imphash = $temp_object_hash["IMPHASH"]
endif

if $Data["Description"] != null then
    object.process.meta = "Description:" + $Data["Description"] + " | Product:" + $Data["Product"] + " | Company:" + $Data["Company"]
endif

object.process.parent.id = $Data["ParentProcessId"]
if lower($Data["ParentImage"]) != "-" then
   $process_parent_fullpath = lower($Data["ParentImage"])
endif
$process_parent_fullpath_csv = csv($process_parent_fullpath, "\\", "")
$process_parent_name = $process_parent_fullpath_csv[length($process_parent_fullpath_csv) - 1]
object.process.parent.name = $process_parent_name
if strip($process_parent_fullpath, "", $process_parent_name) != '' then
    object.process.parent.path = strip($process_parent_fullpath, "", $process_parent_name)
endif
object.process.parent.fullpath = $process_parent_fullpath
object.process.parent.guid = strip($Data['ParentProcessGuid'], '{', '}')
if $Data["ParentCommandLine"] != "-" then
    object.process.parent.cmdline = $Data["ParentCommandLine"]
endif
object.process.cwd = $Data["CurrentDirectory"]

datafield6 = strip($Data['LogonGuid'], '{', '}')  

time = $Data["UtcTime"]
msgid = $EventID
importance = "info"

category.generic = "Process"
category.high = "Availability Management"
category.low = "Control"

event_src.vendor = "microsoft"
event_src.title = "sysmon"
event_src.subsys = $Channel
if $Data["RuleName"] != "-" then
    event_src.rule = $Data["RuleName"]
endif
if find_substr($Computer, '.') != null then
    event_src.fqdn = lower($Computer)
    event_src.hostname = lower(substr($Computer, 0, find_substr($Computer, '.')))
else
    event_src.hostname = lower($Computer)
endif
event_src.category = "Other"

id = "PT_Microsoft_Windows_eventlog_Sysmon_1_Process_creation"

