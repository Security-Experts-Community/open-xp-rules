JSON = ''
COND = (
    $eventid != null
    and $node != null
    and $timestamp != null
    and (
        (
            find_substr($items["SYSCALL"][0], "arch=c000003e") != null
            and (
                find_substr($items["SYSCALL"][0], "syscall=2 ") != null      # open
                or find_substr($items["SYSCALL"][0], "syscall=257") != null  # openat
                or find_substr($items["SYSCALL"][0], "syscall=437") != null  # openat2
                or find_substr($items["SYSCALL"][0], "syscall=85") != null   # creat
            )
        )
        or (
            find_substr($items["SYSCALL"][0], "arch=40000003") != null
            and (
                find_substr($items["SYSCALL"][0], "syscall=5 ") != null      # open
                or find_substr($items["SYSCALL"][0], "syscall=295") != null  # openat
                or find_substr($items["SYSCALL"][0], "syscall=437") != null  # openat2
                or find_substr($items["SYSCALL"][0], "syscall=8 ") != null   # creat
            )
        )
    )
)

$kv_syscall = keyvalue($items['SYSCALL'][0], ' ', '=')

if $kv_syscall["auid"] != "4294967295" then
    subject = "account"
endif

object = "file_object"

if $kv_syscall["success"] == "yes" then
    status = "success"
else
    status = "failure"
endif

# Декодируем HEX, если требуется
$auid_en = strip($kv_syscall["AUID"], '"', '"')
if $auid_en == $kv_syscall["AUID"] then
    $auid_en = hex_to_string($kv_syscall["AUID"])
endif

$auid_list = csv($auid_en, "@", "")

# Декодируем HEX, если требуется
$euid_en = strip($kv_syscall["EUID"], '"', '"')
if $euid_en == $kv_syscall["EUID"] then
    $euid_en = hex_to_string($kv_syscall["EUID"])
endif

$euid_list = csv($euid_en, "@", "")

if $kv_syscall["auid"] != "4294967295" then
    subject.account.name = $auid_list[0]
    subject.account.domain = $auid_list[1]

    subject.account.id = $kv_syscall["auid"]
else
    subject.account.name = $euid_list[0]
    subject.account.domain = $euid_list[1]

    subject.account.id = $kv_syscall["euid"]
endif

subject.account.privileges = $kv_syscall["euid"]
subject.account.session_id = $kv_syscall["ses"]

subject.process.id = $kv_syscall["pid"]
subject.process.parent.id = $kv_syscall["ppid"]

# SYSCALL::exe
if not in_list(["<no_memory>", "<too_long>", "(null)"], $kv_syscall["exe"]) then
    # Декодируем HEX, если требуется
    $exe = strip($kv_syscall["exe"], '"', '"')
    if $exe == $kv_syscall["exe"] then
        $exe = hex_to_string($kv_syscall["exe"])
    endif
endif

$process_path_and_name = csv($exe, "/", "")
$process_name = $process_path_and_name[length($process_path_and_name) - 1]
$process_path = strip($exe, "", $process_name)

subject.process.fullpath = $exe
subject.process.name = $process_name
subject.process.path = $process_path

# CWD
if $items["CWD"] != null then
    $kv_cwd = keyvalue($items['CWD'][0], ' ', '=', '"')
    
    if not in_list(["<no_memory>", "<too_long>", "(null)"], $kv_cwd["cwd"]) then
        # Декодируем HEX, если требуется
        $cwd_raw = strip($kv_cwd["cwd"], '"', '"')
        if $cwd_raw == $kv_cwd["cwd"] then
            $cwd_raw = hex_to_string($kv_cwd["cwd"])
        endif
        
        if substr($cwd_raw, -1, 1) != "/" then
            $cwd = $cwd_raw + "/"
        else
            $cwd = $cwd_raw
        endif
    endif
endif

subject.process.cwd = $cwd_raw

# PROCTITLE
if $items["PROCTITLE"] != null then
    $kv_proctitle = keyvalue($items["PROCTITLE"][0], ' ', '=')

    $process_proctitle = strip($kv_proctitle["proctitle"], '"', '"')
    if $process_proctitle == "(null)" then
        $process_proctitle = null
    elif $process_proctitle == $kv_proctitle["proctitle"] then
        $cmdline_raw = hex_to_string($kv_proctitle["proctitle"])

        # stripping corrupted char
        if $cmdline_raw == $kv_proctitle["proctitle"] then
            $cmdline_raw = hex_to_string(substr($kv_proctitle["proctitle"], 0, 254))
        endif
        if $cmdline_raw == $kv_proctitle["proctitle"] then
            $cmdline_raw = hex_to_string(substr($kv_proctitle["proctitle"], 0, 253))
        endif
        if $cmdline_raw == $kv_proctitle["proctitle"] then
            $cmdline_raw = hex_to_string(substr($kv_proctitle["proctitle"], 0, 252))
        endif

        $process_proctitle = replace($cmdline_raw, " ", "\u0000")
    endif
endif

subject.process.meta = $process_proctitle  # PROCTITLE

# Декодируем HEX, если требуется
$egid_en = strip($kv_syscall["EGID"], '"', '"')
if $egid_en == $kv_syscall["EGID"] then
    $egid_en = hex_to_string($kv_syscall["EGID"])
endif

object.account.group = coalesce($egid_en, $kv_syscall["egid"])

object.account.name = $euid_list[0]
object.account.domain = $euid_list[1]

object.account.id = $kv_syscall["euid"]

# PATH
$path_list = if $items["PATH"] != null then $items["PATH"] else [] endif
 
if (number($kv_syscall["items"]) != length($path_list)) and not in_list(["0", null], $kv_syscall["items"]) then
    drop
endif

$kv_path_0 = keyvalue($path_list[0], ' ', '=')
$kv_path_1 = keyvalue($path_list[1], ' ', '=')
$kv_path_2 = keyvalue($path_list[2], ' ', '=')

if length($path_list) == 1 then
    $item_0_type = coalesce($kv_path_0["nametype"], $kv_path_0["objtype"])
    
    if $item_0_type == "NORMAL" then
        $normal = $kv_path_0
    elif $item_0_type == "PARENT" then
        $parent = $kv_path_0
    elif $item_0_type == "UNKNOWN" then
        $normal = $kv_path_0
    endif
elif length($path_list) == 2 then
    switch $kv_path_0["item"]
        case "0" $parent = $kv_path_0
        case "1" $normal = $kv_path_0
    endswitch
    switch $kv_path_1["item"]
        case "0" $parent = $kv_path_1
        case "1" $normal = $kv_path_1
    endswitch
elif length($path_list) == 3 then
    switch $kv_path_0["item"]
        case "0" $parent = $kv_path_0
        case "1" $normal = $kv_path_0
        case "2" $normal_src = $kv_path_0
    endswitch
    switch $kv_path_1["item"]
        case "0" $parent = $kv_path_1
        case "1" $normal = $kv_path_1
        case "2" $normal_src = $kv_path_1
    endswitch
    switch $kv_path_2["item"]
        case "0" $parent = $kv_path_2
        case "1" $normal = $kv_path_2
        case "2" $normal_src = $kv_path_2
    endswitch
endif

$parent_type = coalesce($parent["nametype"], $parent["objtype"])
$normal_type = coalesce($normal["nametype"], $normal["objtype"])
$normal_src_type = coalesce($normal_src["nametype"], $normal_src["objtype"])

$type = coalesce($normal_type, $parent_type)

action = switch $type
    case "NORMAL"  "access"
    case "CREATE"  "create"
    case "PARENT"  "access"
    case "UNKNOWN" "access"
endswitch

# PATH::PARENT
if not in_list(["<no_memory>", "<too_long>", "(null)", null], $parent["name"]) then
    # Декодируем HEX, если требуется
    $parent_name = strip($parent["name"], '"', '"')
    if $parent_name == $parent["name"] then
        $parent_name = hex_to_string($parent["name"])
    endif

    # Обрабатываем относительные пути
    if (substr($parent_name, 0, 2) == "./") or (substr($parent_name, 0, 1) != "/") then
        $parent_name = $cwd + strip($parent_name, "./", "")
    endif

    # Добавляем в конец '/', если его нет
    if substr($parent_name, -1, 1) != "/" then
        $parent_name = $parent_name + "/"
    endif
    
    $file_path = $parent_name
endif

# PATH::NORMAL/CREATE
if not in_list(["<no_memory>", "<too_long>", "(null)", null], $normal["name"]) then
    # Декодируем HEX, если требуется
    $normal_name = strip($normal["name"], '"', '"')
    if $normal_name == $normal["name"] then
        $normal_name = hex_to_string($normal["name"])
    endif
    
    # Обрабатываем относительные пути
    if ($normal_name == ".") and (substr($normal["mode"], 0, 2) == "04") then
        $normal_name = $cwd_raw
    elif (substr($normal_name, 0, 2) == "./") or (substr($normal_name, 0, 1) != "/") then
        $normal_name = $cwd + strip($normal_name, "./", "")
    endif

    $file_list = csv($normal_name, "/", "")
    $file_fullpath = $normal_name
    $file_name = $file_list[length($file_list)-1]
    $file_path = strip($file_fullpath, "", $file_name)
endif

object.fullpath = $file_fullpath
object.path = $file_path
object.name = $file_name
object.id = $normal["inode"]

# PATH::NORMAL (symlink case)
if not in_list(["<no_memory>", "<too_long>", "(null)", null], $normal_src["name"]) then
    # Декодируем HEX, если требуется
    $normal_src_name = strip($normal_src["name"], '"', '"')
    if $normal_src_name == $normal_src["name"] then
        $normal_src_name = hex_to_string($normal_src["name"])
    endif
    
    # Обрабатываем относительные пути
    if (substr($normal_src_name, 0, 2) == "./") or (substr($normal_src_name, 0, 1) != "/") then
        $normal_src_name = $cwd + strip($normal_src_name, "./", "")
    endif
    
    $file_src_list = csv($normal_src_name, "/", "")
    $file_src_fullpath = $normal_src_name
    $file_src_name = $file_src_list[length($file_src_list)-1]
    $file_src_path = strip($file_src_fullpath, "", $file_src_name)
endif

# Информация об исходном файле
object.storage.fullpath = $file_src_fullpath
object.storage.path = $file_src_path
object.storage.name = $file_src_name
object.storage.id = $normal_src["inode"]

# PATH::mode
$file_type = switch substr($normal["mode"], 0, length($normal["mode"]) - 4)
    case "01"  "FIFO"
    case "02"  "character_device"
    case "04"  "directory"
    case "06"  "block_device"
    case "010" "regular_file"
    case "012" "symlink"
    case "014" "socket"
endswitch

$u_triplet = number(substr($normal["mode"], -3, 1))
$g_triplet = number(substr($normal["mode"], -2, 1))
$o_triplet = number(substr($normal["mode"], -1, 1))

if ($file_type == "regular_file") and ((mod($u_triplet, 2) == 1) or (mod($g_triplet, 2) == 1) or (mod($o_triplet, 2) == 1)) then
    $file_type = "executable_file"
endif

if $file_type == "symlink" then
    object = "link"
endif

object.type = $file_type

# SYSCALL::a1/a2
if in_list(["2", "5"], $kv_syscall["syscall"]) then
    $mode = $kv_syscall["a1"]
elif in_list(["257", "295", "437"], $kv_syscall["syscall"]) then
    $mode = $kv_syscall["a2"]
endif
$mask = mod(number16($mode), 4)

object.state = switch $mask
    case 0 "r"
    case 1 "w"
    case 2 "rw"
endswitch

$flags_number = number16($mode)

$col_1 = mod($flags_number, 4)
$flag_col_1 = switch $col_1
    case 0 "O_RDONLY"
    case 1 "O_WRONLY"
    case 2 "O_RDWR"
    case 3 "O_ACCMODE"
endswitch

$flags_number = div($flags_number, 8)
$flags_number = div($flags_number, 8)

$col_3 = mod($flags_number, 8)
$flag_col_3 = switch $col_3
    case 0 null
    case 1 "O_CREAT"
    case 2 "O_EXCL"
    case 3 "O_EXCL|O_CREAT"
    case 4 "O_NOCTTY"
    case 5 "O_NOCTTY|O_CREAT"
    case 6 "O_NOCTTY|O_EXCL"
    case 7 "O_NOCTTY|O_EXCL|O_CREAT"
endswitch

$flags_number = div($flags_number, 8)

$col_4 = mod($flags_number, 8)
$flag_col_4 = switch $col_4
    case 0 null
    case 1 "O_TRUNC"
    case 2 "O_APPEND"
    case 3 "O_APPEND|O_TRUNC"
    case 4 "O_NONBLOCK"
    case 5 "O_NONBLOCK|O_TRUNC"
    case 6 "O_NONBLOCK|O_APPEND"
    case 7 "O_NONBLOCK|O_APPEND|O_TRUNC"
endswitch

$flags_number = div($flags_number, 8)

$col_5 = mod($flags_number, 8)
$flag_col_5 = switch $col_5
    case 0 null
    case 1 "O_DSYNC"
    case 2 "FASYNC"
    case 3 "FASYNC|O_DSYNC"
    case 4 "O_DIRECT"
    case 5 "O_DIRECT|O_DSYNC"
    case 6 "O_DIRECT|FASYNC"
    case 7 "O_DIRECT|FASYNC|O_DSYNC"
endswitch

$flags_number = div($flags_number, 8)

$col_6 = mod($flags_number, 8)
$flag_col_6 = switch $col_6
    case 0 null
    case 1 "O_LARGEFILE"
    case 2 "O_DIRECTORY"
    case 3 "O_DIRECTORY|O_LARGEFILE"
    case 4 "O_NOFOLLOW"
    case 5 "O_NOFOLLOW|O_LARGEFILE"
    case 6 "O_NOFOLLOW|O_DIRECTORY"
    case 7 "O_NOFOLLOW|O_DIRECTORY|O_LARGEFILE"
endswitch

$flags_number = div($flags_number, 8)

$col_7 = mod($flags_number, 8)
$flag_col_7 = switch $col_7
    case 0 null
    case 1 "O_NOATIME"
    case 2 "O_CLOEXEC"
    case 3 "O_CLOEXEC|O_NOATIME"
    case 4 "__O_SYNC"
    case 5 "__O_SYNC|O_NOATIME"
    case 6 "__O_SYNC|O_CLOEXEC"
    case 7 "__O_SYNC|O_CLOEXEC|O_NOATIME"
endswitch

$flags_number = div($flags_number, 8)

$col_8 = mod($flags_number, 8)
$flag_col_8 = switch $col_8
    case 0 null
    case 1 "O_PATH"
    case 2 "__O_TMPFILE"
    case 3 "__O_TMPFILE|O_PATH"
endswitch

object.property = "flags"

$obj_value = join(remove([$flag_col_1, $flag_col_3, $flag_col_4, $flag_col_5, $flag_col_6, $flag_col_7, $flag_col_8], null), "|")

if in_list(["8", "85"], $kv_syscall["syscall"]) then
    object.value = "O_CREAT|O_WRONLY|O_TRUNC"
else
    object.value = $obj_value
endif

if $kv_syscall["tty"] != "(none)" then
    $tty = $kv_syscall["tty"]

    if (substr($tty, 0, 3) == "pts") and (substr($tty, 3, 1) != "/") then
        logon_service = "pts/" + substr($tty, 3, length($tty) - 3)
    else
        logon_service = $tty
    endif
endif

# Декодируем HEX, если требуется
$comm = strip($kv_syscall["comm"], '"', '"')
if $comm == $kv_syscall["comm"] then
    $comm = hex_to_string($kv_syscall["comm"])
endif

datafield1 = $comm  # Executable Name

# Декодируем HEX, если требуется
$ouid_en = strip($normal["OUID"], '"', '"')
if $ouid_en == $normal["OUID"] then
    $ouid_en = hex_to_string($normal["OUID"])
endif

datafield2 = $ouid_en  # Owner User Name
datafield3 = substr($normal["mode"], -4, 4)  # File Access Mode

numfield2 = $normal["ouid"]  # Owner User ID

chain_id = $eventid  # AuditD Event ID

reason = switch $kv_syscall["exit"]
    case "-1"   "Operation not permitted"
    case "-2"   "No such file or directory"
    case "-4"   "Interrupted system call"
    case "-6"   "No such device or address"
    case "-9"   "Bad file descriptor"
    case "-11"  "Resource temporarily unavailable"
    case "-12"  "Cannot allocate memory"
    case "-13"  "Permission denied"
    case "-14"  "Bad address"
    case "-16"  "Device or resource busy"
    case "-17"  "File exists"
    case "-19"  "No such device"
    case "-20"  "Not a directory"
    case "-21"  "Is a directory"
    case "-22"  "Invalid argument"
    case "-23"  "Too many open files in system"
    case "-24"  "Too many open files"
    case "-26"  "Text file busy"
    case "-27"  "File too large"
    case "-28"  "No space left on device"
    case "-30"  "Read-only file system"
    case "-36"  "File name too long"
    case "-40"  "Too many levels of symbolic links"
    case "-75"  "Value too large for defined data type"
    case "-122" "Disk quota exceeded"
    case "-123" "No medium found"
endswitch

time = epoch_to_datetime(number($timestamp))

msgid = switch $kv_syscall["syscall"]
    case "2"   "open"
    case "5"   "open"
    case "257" "openat"
    case "295" "openat"
    case "437" "openat2"
    case "8"   "creat"
    case "85"  "creat"
endswitch

importance = "low"

category.generic = switch object
    case "file_object" "File System Object"
    case "link" "Link"
endswitch

category.high = "System Management"
category.low = "Manipulation"

event_src.title = "unix_like"
event_src.subsys = "auditd"

# Декодируем HEX, если требуется
$key_raw = strip($kv_syscall["key"], '"', '"')
if $key_raw == $kv_syscall["key"] then
    $key_raw = hex_to_string($kv_syscall["key"])
endif

event_src.rule = if $key_raw != "(null)" then $key_raw else null endif

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
        else
            event_src.ip = recv_ipv4
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
        else
            event_src.hostname = $node
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

event_src.category = "Operating system"

id = "PT_UNIX_like_auditd_syslog_structured_syscall_file_operations"
