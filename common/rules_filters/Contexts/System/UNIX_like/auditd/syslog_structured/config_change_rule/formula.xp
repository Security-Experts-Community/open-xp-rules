JSON = ''
COND = (
    $eventid != null
    and $node != null
    and $timestamp != null
    and $items["CONFIG_CHANGE"] != null
    and (
        find_substr($items["CONFIG_CHANGE"][0], "op=remove_rule") != null
        or find_substr($items["CONFIG_CHANGE"][0], "op=add_rule") != null
    )
)

$kv_main = keyvalue($items["CONFIG_CHANGE"][0], " ", "=", "'")
$kv_syscall = keyvalue($items["SYSCALL"][0], " ", "=", "'")

if $kv_main["auid"] != "4294967295" then
    subject = "account"
endif

action = switch $kv_main["op"]
    case "add_rule" "create"
    case "remove_rule" "remove"
endswitch

object = "rule"

status = switch $kv_main["res"]
    case "1" "success"
    case "0" "failure"
endswitch

# Декодируем HEX, если требуется
$auid_en = strip($kv_main["AUID"], '"', '"')
if $auid_en == $kv_main["AUID"] then
    $auid_en = hex_to_string($kv_main["AUID"])
endif

$auid_list = csv($auid_en, "@", "")

if $kv_main["auid"] != "4294967295" then
    subject.account.name = $auid_list[0]
    subject.account.domain = $auid_list[1]

    subject.account.id = $kv_main["auid"]
endif

subject.account.session_id = $kv_main["ses"]
subject.account.privileges = $kv_syscall["euid"]

# SYSCALL::exe
if not in_list(["<no_memory>", "<too_long>", "(null)"], $kv_syscall["exe"]) then
    # Декодируем HEX, если требуется
    $exe = strip($kv_syscall["exe"], '"', '"')
    if $exe == $kv_syscall["exe"] then
        $exe = hex_to_string($kv_syscall["exe"])
    endif
endif

$process_path_and_name = csv($exe, "/", "")
$process_fullpath = $exe
$process_name = $process_path_and_name[length($process_path_and_name) - 1]
$process_path = strip($exe, "", $process_name)

subject.process.fullpath = $process_fullpath
subject.process.name = $process_name
subject.process.path = $process_path

# PROCTITLE
if $items["PROCTITLE"] != null then
    $kv_proctitle = keyvalue($items["PROCTITLE"][0], ' ', '=')
 
    $process_proctitle = strip($kv_proctitle["proctitle"], '"', '"')
    if $process_proctitle == "(null)" then
        $process_proctitle = null
    elif $process_proctitle == $kv_proctitle["proctitle"] then
        $cmdline_raw = hex_to_string($kv_proctitle["proctitle"])
 
        # stripping corrupted char
        if $cmdline_raw == $kv_proctitle["proctitle"] then
            $cmdline_raw = hex_to_string(substr($kv_proctitle["proctitle"], 0, 254))
        endif
        if $cmdline_raw == $kv_proctitle["proctitle"] then
            $cmdline_raw = hex_to_string(substr($kv_proctitle["proctitle"], 0, 253))
        endif
        if $cmdline_raw == $kv_proctitle["proctitle"] then
            $cmdline_raw = hex_to_string(substr($kv_proctitle["proctitle"], 0, 252))
        endif
 
        $process_proctitle = replace($cmdline_raw, " ", "\u0000")
    endif
endif

subject.process.meta = $process_proctitle  # PROCTITLE

subject.process.id = $kv_syscall["pid"]
subject.process.parent.id = $kv_syscall["ppid"]

object.name = strip($kv_main["key"], '"', '"')

if $items["PATH"] != null then
    object.type = "file_monitoring"
    
    $kv_path = keyvalue($items["PATH"][0], ' ', '=', '"')
    
    object.property = "path_to_file"
    object.value = $kv_path["name"]
endif

# Декодируем HEX, если требуется
$egid_en = strip($kv_syscall["EGID"], '"', '"')
if $egid_en == $kv_syscall["EGID"] then
    $egid_en = hex_to_string($kv_syscall["EGID"])
endif

object.account.group = coalesce($egid_en, $kv_syscall["egid"])
object.account.name = strip($kv_syscall["EUID"], '"', '"')
object.account.id = $kv_syscall["euid"]

if $kv_syscall["tty"] != "(none)" then
    $tty = $kv_syscall["tty"]

    if (substr($tty, 0, 3) == "pts") and (substr($tty, 3, 1) != "/") then
        logon_service = "pts/" + substr($tty, 3, length($tty) - 3)
    else
        logon_service = $tty
    endif
endif

chain_id = $eventid  # AuditD Event ID

time = epoch_to_datetime(number($timestamp))
msgid = "CONFIG_CHANGE"
importance = "low"

event_src.title = "unix_like"
event_src.subsys = "auditd"
event_src.category = "Operating system"

if $node != "" then
    if maybe_ipv4($node) != null or maybe_ipv6($node) != null then
        if (in_subnet($node, "127.0.0.0/8") == False and $node != "::1") then
            event_src.ip = $node
            labels = "node_contains_ext_ip"
        endif
    else
        $first_dot = find_substr($node, ".")
        if $first_dot != null then
            event_src.fqdn = $node
            event_src.hostname = substr($node, 0, $first_dot)
        else
            event_src.hostname = $node
        endif
    endif
else
    event_src.ip = recv_ipv4
endif

id = "PT_UNIX_like_auditd_syslog_structured_config_change_rule"
